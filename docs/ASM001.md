## Sample Program ASM001

### Source

DWALL01.DEV.SORLIB(ASM001) sends a message to the operator console.

This program introduces basic standards that we'll follow throughout. It begin with "flower box" documentation that names and describes the program and expected register values on entry and exit. The "USING" statement tells the assembler to use a specific register or registers as the base address for labels in a section. The "CSECT" statement starts a control section which includes code and constant values. The "DROP" statement releases the register or section from the assembler's use of it as a base register. The "END" statement informs the assembler that we have reached the end of the program and identifies the control section that starts the program.

On entry to the program, register 15 holds the address of the start of the program. Since we're using register 12 as our base register, we immediately load it with the value of register 15, using the LR (Load Register) instruction, so that it matches the base address used to compute address operands in the section. If we wanted to, we could have specified "USING @PGM,15" and not used register 12 at all. But, by convention, return values are placed in register 15. Also, some system services change the value of register 15. So it's a good practice to use registers other than 15 as base registers. The S/370 base + displacement address operand formats (RX, RS, S, SI, SS) limit the scope of a single base register to 2^12 or 4KB. So, large programs often used multiple base registers to span a large control section (CSECT). We'll adopt a different usage standard in these samples to help overcome this limitation.

The program invokes the "WTO" macro. This sends a constant string to the operator console. This is for convenience as we get started since we have access to the web console. In the real world, your program would probably not send messages to the console unless it required operator intervention, which should be minimal if at all. In later samples we'll send messages to a log file or to the SYSPRINT DD instead of the console.

The "XR" instruction (Exclusive OR) is used here to set the value of register 15 to zero, which, by convention, means that no error occurred during the execution of the program. Since the value of register 15 is displayed at the operator console when the program returns control, we want to make sure we don't inadvertently return a non-zero value unless an error is really detected.

The program exits by branching to the address in register 14 with the "BR 14" instruction. The system provided this return address at the start of the program. It's the program's responsibility to keep track of this address. This program, including the WTO macro, doesn't modify register 14 or call any routine that does. So, we don't have to take any action to save its value yet. The WTO macro expands to invoke a supervisor call (SVC) instruction. Programming conventions for SVC calls state that registers 2 through 14 are preserved.
```
***********************************************************************
*
*        PROGRAM    ASM001
*
*        THIS SAMPLE PROGRAM ILLUSTRATES THE USING, DROP, CSECT AND
*        END STATEMENTS AND ISSUES A WTO MACRO TO SEND A MESSAGE TO
*        THE OPERATOR CONSOLE BEFORE RETURNING TO THE CALLER.
*
*        ENTRY      R1      PROGRAM ARGUMENTS ADDRESS
*                   R13     CALLER SAVE AREA ADDRESS
*                   R14     RETURN ADDRESS
*                   R15     CONTROL SECTION ADDRESS
*
*        EXIT       R14     RETURN ADDRESS
*                   R15     RESULT CODE
*
***********************************************************************
@PGM     CSECT                          PROGRAM ENTRY
         USING @PGM,12                  RESOLVE @PGM TO R12
         LR    12,15                    @PGM BASE
         WTO   'HELLO, WORLD!'          NOTIFY CONSOLE
         XR    15,15                    ZERO RESULT CODE
         BR    14                       RETURN TO CALLER
         DROP  12                       RELEASE R12 USAGE
         END   @PGM                     END OF PROGRAM
```

### Job Control Language

DWALL01.DEV.JCLLIB(ASM001) assembles, links and runs the program.

JS010 runs the IFOX00 program to assemble the source code into an object deck.  
JS020 runs the IEWL program to read the object deck and produce an executable load module.  
JS030 runs the load module.
```
//ASM001   JOB (001),'DWALL01',CLASS=A,MSGCLASS=A,
//             MSGLEVEL=(1,1),NOTIFY=DWALL01
//*
//*            ASSEMBLE THE PROGRAM
//*
//JS010   EXEC PGM=IFOX00,REGION=256K,
//             PARM='OBJ,RENT,XREF'
//SYSLIB    DD DISP=SHR,DSN=SYS1.MACLIB
//          DD DISP=SHR,DSN=DWALL01.DEV.MACLIB
//SYSUT1    DD DSN=&&SYSUT1,UNIT=SYSSQ,
//             SPACE=(1700,(600,100)),SEP=(SYSLIB)
//SYSUT2    DD DSN=&&SYSUT2,UNIT=SYSSQ,
//             SPACE=(1700,(300,50)),SEP=(SYSUT1)
//SYSUT3    DD DSN=&&SYSUT3,UNIT=SYSSQ,
//             SPACE=(1700,(300,50))
//SYSPRINT  DD DISP=SHR,DSN=DWALL01.DEV.SYSPRINT(IFOX00)
//SYSPUNCH  DD SYSOUT=B
//SYSGO     DD DISP=SHR,DSN=DWALL01.DEV.OBJLIB(ASM001)
//SYSIN     DD DISP=SHR,DSN=DWALL01.DEV.SORLIB(ASM001)
//*
//*            LINK THE PROGRAM
//*
//JS020   EXEC PGM=IEWL,REGION=256K,COND=(0,LT),
//             PARM='XREF,LET,LIST,NCAL,RENT'
//SYSUT1    DD DSN=&&SYSUT1,UNIT=(SYSDA,SEP=(SYSLIN,SYSLMOD)),
//             SPACE=(1024,(50,20))
//SYSPRINT  DD DISP=SHR,DSN=DWALL01.DEV.SYSPRINT(IEWL)
//SYSLMOD   DD DISP=SHR,DSN=DWALL01.DEV.MODLIB(ASM001)
//SYSLIN    DD DISP=SHR,DSN=DWALL01.DEV.OBJLIB(ASM001)
//SYSIN     DD *
NAME ASM001(R)
/*
//*
//*            RUN THE PROGRAM
//*
//JS030   EXEC PGM=ASM001,REGION=256K,COND=(0,LT)
//STEPLIB   DD DISP=SHR,DSN=DWALL01.DEV.MODLIB
//SYSPRINT  DD SYSOUT=*
//SYSABEND  DD SYSOUT=*
//SYSUDUMP  DD SYSOUT=*
//
```

### Assembly Listing

DWALL01.DEV.SYSPRINT(IFOX00) contains the assembler listing of our program.

WTO expands to use  BAL (branch and link) and SVC (supervisor call) instructions. BAL works by loading the named register with the value of the next sequential instruction (NSI) so that a called routine can use that register to return. Here, though, register 1 isn't used to return. Instead it's used as the address of a parameter list to the SVC instruction. The parameters are a half-word length field, a half-word flags field and the message text itself. The target of the BAL instruction is the label IHB0001A, which is system generated. The label names a define storage (DS) instruction that reserves zero half-words. This is a technique to ensure that the next instruction is encoded on a half-word boundary, which is required by the processor to avoid a System Completion Code 6 (S0C6) abnormal end (ABEND) termination of the program.
```
                                               EXTERNAL SYMBOL DICTIONARY                                      PAGE    1
SYMBOL   TYPE  ID   ADDR  LENGTH LDID                                                            ASM 0201 13.29 12/17/20
@PGM      SD  0001 000000 000020                                                                                        
                                                                                                               PAGE    2
  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 13.29 12/17/20
                                      1 ***********************************************************************         
                                      2 *                                                                               
                                      3 *        PROGRAM    ASM001                                                      
                                      4 *                                                                               
                                      5 *        THIS SAMPLE PROGRAM ILLUSTRATES THE USING, DROP, CSECT AND             
                                      6 *        END STATEMENTS AND ISSUES A WTO MACRO TO SEND A MESSAGE TO             
                                      7 *        THE OPERATOR CONSOLE BEFORE RETURNING TO THE CALLER.                   
                                      8 *                                                                               
                                      9 *        ENTRY      R1      PROGRAM ARGUMENTS ADDRESS                           
                                     10 *                   R13     CALLER SAVE AREA ADDRESS                            
                                     11 *                   R14     RETURN ADDRESS                                      
                                     12 *                   R15     CONTROL SECTION ADDRESS                             
                                     13 *                                                                               
                                     14 *        EXIT       R14     RETURN ADDRESS                                      
                                     15 *                   R15     RESULT CODE                                         
                                     16 *                                                                               
                                     17 ***********************************************************************         
000000                               18 @PGM     CSECT                          PROGRAM ENTRY                           
                            00000    19          USING @PGM,12                  RESOLVE @PGM TO R12                     
000000 18CF                          20          LR    12,15                    @PGM BASE                               
                                     21          WTO   'HELLO, WORLD!'          NOTIFY CONSOLE                          
000002 0700                          22+         CNOP  0,4                                                      09800002
000004 4510 C01A      0001A          23+         BAL   1,IHB0001A                        BRANCH AROUND MESSAGE  09850002
000008 0011                          24+         DC    AL2(17)             TEXT LENGTH                          13200002
00000A 0000                          25+         DC    B'0000000000000000' MCS FLAGS                            13250002
00000C C8C5D3D3D66B40E6              26+         DC    C'HELLO, WORLD!'                                         13350002
00001A                               27+IHB0001A DS    0H                                                       15850002
00001A 0A23                          28+         SVC   35                                                       15950002
00001C 17FF                          29          XR    15,15                    ZERO RESULT CODE                        
00001E 07FE                          30          BR    14                       RETURN TO CALLER                        
                                     31          DROP  12                       RELEASE R12 USAGE                       
000000                               32          END   @PGM                     END OF PROGRAM                          
                                                  CROSS-REFERENCE                                              PAGE    3
SYMBOL    LEN   VALUE   DEFN    REFERENCES                                                       ASM 0201 13.29 12/17/20
@PGM     00001 00000000 00018  00019 00032                                                                              
IHB0001A 00002 0000001A 00027  00023                                                                                    
                                                  ASSEMBLER DIAGNOSTICS AND STATISTICS                         PAGE    4
                                                                                                 ASM 0201 13.29 12/17/20
NO STATEMENTS FLAGGED IN THIS ASSEMBLY                                                                                  
HIGHEST SEVERITY WAS    0                                                                                               
OPTIONS FOR THIS ASSEMBLY                                                                                               
  ALIGN, ALOGIC, BUFSIZE(STD), DECK, ESD, FLAG(0), LINECOUNT(55), LIST, NOMCALL, YFLAG, WORKSIZE(2097152)               
  NOMLOGIC, NONUMBER, OBJECT, RENT, RLD, NOSTMT, NOLIBMAC, NOTERMINAL, NOTEST, XREF                                     
  SYSPARM()                                                                                                             
WORK FILE BUFFER SIZE/NUMBER =19066/ 1                                                                                  
TOTAL RECORDS READ FROM SYSTEM INPUT         25                                                                         
TOTAL RECORDS READ FROM SYSTEM LIBRARY     1064                                                                         
TOTAL RECORDS PUNCHED                         4                                                                         
TOTAL RECORDS PRINTED                        54                                                                         
```

### Linker Messages

DWALL01.DEV.SYSPRINT(IEWL) contains the linker output messages.

The linker produces an executable program. It has identified one CSECT, "@PGM" and an entry address of 0. The module is marked "reenterable" and "reusable" indicating that its use of registers and data does not preclude multiple tasks running the program concurrently.
```
 F64-LEVEL LINKAGE EDITOR OPTIONS SPECIFIED XREF,LET,LIST,NCAL,RENT                                                  
          DEFAULT OPTION(S) USED -  SIZE=(229376,55296)                                                              
                                                                                                                     
                                                CROSS REFERENCE TABLE                                                
                                                                                                                     
  CONTROL SECTION                       ENTRY                                                                        
    NAME    ORIGIN  LENGTH                NAME   LOCATION     NAME   LOCATION     NAME   LOCATION     NAME   LOCATION
  @PGM          00      20                                                                                           
                                                                                                                     
                                                                                                                     
 ENTRY ADDRESS       00                                                                                              
 TOTAL LENGTH        20                                                                                              
****ASM001    NOW REPLACED IN DATA SET                                                                               
AUTHORIZATION CODE IS         0.                                                                                     
**MODULE HAS BEEN MARKED REENTERABLE, AND REUSABLE.                                                                  
```

### Console Output

The console output produced by running our job is visible in the Hercules web console. Note the "HELLO, WORLD!" message in the console log prior to the completion of JS030, the job step running our program.
```
13.29.45 JOB  148  $HASP100 ASM001   ON INTRDR      DWALL01
13.29.45 JOB  148  $HASP373 ASM001   STARTED - INIT  1 - CLASS A - SYS HMVS
13.29.45 JOB  148  IEF403I ASM001 - STARTED - TIME=13.29.45
13.29.46 JOB  148  IEFACTRT JS010   /IFOX00  /00:00:00.70/00:00:01.39/00000/ASM001
13.29.47 JOB  148  IEFACTRT JS020   /IEWL    /00:00:00.23/00:00:00.41/00000/ASM001
13.29.47 JOB  148  +HELLO, WORLD!
13.29.47 JOB  148  IEFACTRT JS030   /ASM001  /00:00:00.00/00:00:00.16/00000/ASM001
13.29.47 JOB  148  IEF404I ASM001 - ENDED - TIME=13.29.47
13.29.47 JOB  148  $HASP395 ASM001   ENDED
13.29.47           $HASP309    INIT  1 INACTIVE ******** C=A
13.29.47 JOB  148  $HASP150 ASM001   ON PUNCH1           4 CARDS
13.29.47 JOB  148  $HASP150 ASM001   ON PRINTER1       155 LINES
13.29.47           $HASP160 PUNCH1   INACTIVE - CLASS=B
13.29.47           $HASP160 PRINTER1 INACTIVE - CLASS=A
13.29.47 JOB  148  $HASP250 ASM001   IS PURGED
```

<hr>

Return to [Sample Programs](Samples.md)  
Return to [README](../README.md)
