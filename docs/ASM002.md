## Sample Program ASM002

### Source

DWALL01.DEV.SORLIB(ASM002) sends a message to the operator console.

Our second program builds on the first. Each section (CSECT and DSECT) will have a small flower-box to make it easier to locate and identify. We've added SPACE and EJECT statements to improve the readability of our assembler listing, the output from the IFOX00 program, which replaces "SPACE" with a blank line and "EJECT" with the start of a new page. By convention, we group similar or related sequential statements and place blank lines before and after labels, macros, service calls, etc. "Working Storage" is what we call that part of a control section and related data section (DSECT) that defines symbolic equates and constants in a CSECT and variable fullwords, half-words, and character sequences in a DSECT. The "COPY" statement is similar to an "#include" statement in a C or C++ program. It includes another partitioned data set (PDS) member's contents into the program stream. This is useful if there are common definitions to include in multiple programs. The assembler searches one or more data sets listed in the SYSLIB DD concatenation in the JCL job step that runs IFOX00 for a data set with a member matching the name in the "COPY" statement. In this program, the "copy book" called EREGS defines symbolic names for the general registers, R0 for 0, R1 for 1, etc. This convention aids readability and helps ensure the programmer's intent. The "LTORG" statement instructs the assembler to generate constant defintions for any literal constant expressed in the source code. For example, coding "=X'23'" as an operand requires that the program CSECT define a byte of storage with that value. All such values are defined at LTORG in what is known as the "literal pool". It can be placed practically anywhere in the CSECT provided that it does not interrupt sequential instruction flow. By convention, I place it after all named constants.

At the start of program execution, register 13 holds the address of the calling program's save area. The intent is to provide a called program some immediately available storage area so that the contents of the caller's registers can be saved and then restored on exit, as the S/370 instruction set does not provide intrinsic support for a "stack" segment. The value of multiple registers can be stored with the STM (store multiple) instruction and loaded with LM (load multiple). These instructions allow for specifying which registers to store or load in the form of a range which wraps back to zero after 15. So our statement "STM R14,R12,12(R13)" means store the values of registers 14 through 12 (wrapping to zero at 15) at the address of offset 12 bytes from the value of register 13 (the save area). In other words, all registers except register 13 are stored. At the end of the program, an LM instruction reloads register values from the same storage area with "LM R0,R12,20(R13)", meaning load the values from an offset of 20 from the storage area at register 13 into registers 0 through 12. Registers 13, 14 and 15 are not loaded by this instruction. We mean to skip loading 13 and 15 because 13 wasn't stored and 15 is our own program's return value. We do need to restore register 14 however, since that is our program's return address. But since we had to skip 13, we need a separate instruction, "L R14,12(R13)" to restore it. All other instructions are the same as in ASM001.
```
***********************************************************************
*
*        PROGRAM    ASM002
*
*        THIS SAMPLE ILLUSTRATES THE COPY, SPACE, EJECT AND LTORG
*        STATEMENTS, BUILDING ON THE PREVIOUS SAMPLE, ADDING LOGIC
*        TO SAVE THE CALLING PROGRAM REGISTER VALUES ON ENTRY AND
*        RESTORING THEM ON EXIT. THE COPY STATEMENT INCLUDES THE
*        EREGS MEMBER FROM A COPY BOOK DATA SET LISTED IN THE
*        ASSEMBLY JOB'S SYSLIB CONCATENATION. THE EREGS MEMBER
*        DEFINES SYMBOLIC NAMES FOR THE GENERAL PURPOSE REGISTERS.
*
*        ENTRY      R1      PROGRAM ARGUMENTS ADDRESS
*                   R13     CALLER SAVE AREA ADDRESS
*                   R14     RETURN ADDRESS
*                   R15     CONTROL SECTION ADDRESS
*
*        EXIT       R14     RETURN ADDRESS
*                   R15     RESULT CODE
*
***********************************************************************
         EJECT
***********************************************************************
*
*        @PGM       PROGRAM CONTROL SECTION
*
***********************************************************************
         SPACE
@PGM     CSECT                          PROGRAM ENTRY
         SPACE
         USING @PGM,R12
         SPACE
         STM   R14,R12,12(R13)          SAVE CALLER REGS
         LR    R12,R15                  @PGM BASE
         SPACE
         WTO   'HELLO, WORLD!'          NOTIFY CONSOLE
         SPACE
         XR    R15,R15                  ZERO RESULT CODE
         SPACE
         LM    R0,R12,20(R13)           LOAD CALLER REGS
         L     R14,12(,R13)             LOAD RETURN ADDR
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R12
         EJECT
***********************************************************************
*
*        W O R K I N G   S T O R A G E
*
***********************************************************************
         SPACE
         COPY  EREGS
         EJECT
         LTORG
         END   @PGM                     END OF PROGRAM
```

### Copy Book Members

DWALL01.DEV.MACLIB(EREGS) is a reusable copy book that provides symbolic names to general register numbers. Since EREGS will usually be the first copy book included that defines EQUATES, it includes a documentation banner for the entire equates section.
```
***********************************************************************
*
*        EQUATES
*
***********************************************************************
         SPACE
*                                       *******************************
*                                       *  REGISTERS
*                                       *******************************
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
```

### Job Control Language

DWALL01.DEV.JCLLIB(ASM002) assembles, links and runs the program. Note the SYSLIB DD concatenation for JS010 includes our copy book PDS, DWALL01.DEV.MACLIB.

JS010 runs the IFOX00 program to assemble the source code into an object deck.  
JS020 runs the IEWL program to read the object deck and produce an executable load module.  
JS030 runs the load module.
```
//ASM002   JOB (001),'DWALL01',CLASS=A,MSGCLASS=A,
//             MSGLEVEL=(1,1),NOTIFY=DWALL01
//*
//*            ASSEMBLE THE PROGRAM
//*
//JS010   EXEC PGM=IFOX00,REGION=256K,
//             PARM='OBJ,RENT,XREF'
//SYSLIB    DD DISP=SHR,DSN=SYS1.MACLIB
//          DD DISP=SHR,DSN=DWALL01.DEV.MACLIB
//SYSUT1    DD DSN=&&SYSUT1,UNIT=SYSSQ,
//             SPACE=(1700,(600,100)),SEP=(SYSLIB)
//SYSUT2    DD DSN=&&SYSUT2,UNIT=SYSSQ,
//             SPACE=(1700,(300,50)),SEP=(SYSUT1)
//SYSUT3    DD DSN=&&SYSUT3,UNIT=SYSSQ,
//             SPACE=(1700,(300,50))
//SYSPRINT  DD DISP=SHR,DSN=DWALL01.DEV.SYSPRINT(IFOX00)
//SYSPUNCH  DD SYSOUT=B
//SYSGO     DD DISP=SHR,DSN=DWALL01.DEV.OBJLIB(ASM002)
//SYSIN     DD DISP=SHR,DSN=DWALL01.DEV.SORLIB(ASM002)
//*
//*            LINK THE PROGRAM
//*
//JS020   EXEC PGM=IEWL,REGION=256K,COND=(0,LT),
//             PARM='XREF,LET,LIST,NCAL,RENT'
//SYSUT1    DD DSN=&&SYSUT1,UNIT=(SYSDA,SEP=(SYSLIN,SYSLMOD)),
//             SPACE=(1024,(50,20))
//SYSPRINT  DD DISP=SHR,DSN=DWALL01.DEV.SYSPRINT(IEWL)
//SYSLMOD   DD DISP=SHR,DSN=DWALL01.DEV.MODLIB(ASM002)
//SYSLIN    DD DISP=SHR,DSN=DWALL01.DEV.OBJLIB(ASM002)
//SYSIN     DD *
NAME ASM002(R)
/*
//*
//*            RUN THE PROGRAM
//*
//JS030   EXEC PGM=ASM002,REGION=256K,COND=(0,LT)
//STEPLIB   DD DISP=SHR,DSN=DWALL01.DEV.MODLIB
//SYSPRINT  DD SYSOUT=*
//SYSABEND  DD SYSOUT=*
//SYSUDUMP  DD SYSOUT=*
//
```

### Assembly Listing

No literals were used in the program, so the literal pool (LTORG) is empty.
```
                                               EXTERNAL SYMBOL DICTIONARY                                      PAGE    1
SYMBOL   TYPE  ID   ADDR  LENGTH LDID                                                            ASM 0201 17.53 12/17/20
@PGM      SD  0001 000000 000030                                                                                        
                                                                                                               PAGE    2
  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 17.53 12/17/20
                                      1 ***********************************************************************         
                                      2 *                                                                               
                                      3 *        PROGRAM    ASM002                                                      
                                      4 *                                                                               
                                      5 *        THIS SAMPLE ILLUSTRATES THE COPY, SPACE, EJECT AND LTORG               
                                      6 *        STATEMENTS, BUILDING ON THE PREVIOUS SAMPLE, ADDING LOGIC              
                                      7 *        TO SAVE THE CALLING PROGRAM REGISTER VALUES ON ENTRY AND               
                                      8 *        RESTORING THEM ON EXIT. THE COPY STATEMENT INCLUDES THE                
                                      9 *        EREGS MEMBER FROM A COPY BOOK DATA SET LISTED IN THE                   
                                     10 *        ASSEMBLY JOB'S SYSLIB CONCATENATION. THE EREGS MEMBER                  
                                     11 *        DEFINES SYMBOLIC NAMES FOR THE GENERAL PURPOSE REGISTERS.              
                                     12 *                                                                               
                                     13 *        ENTRY      R1      PROGRAM ARGUMENTS ADDRESS                           
                                     14 *                   R13     CALLER SAVE AREA ADDRESS                            
                                     15 *                   R14     RETURN ADDRESS                                      
                                     16 *                   R15     CONTROL SECTION ADDRESS                             
                                     17 *                                                                               
                                     18 *        EXIT       R14     RETURN ADDRESS                                      
                                     19 *                   R15     RESULT CODE                                         
                                     20 *                                                                               
                                     21 ***********************************************************************         
                                                                                                               PAGE    3
  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 17.53 12/17/20
                                     23 ***********************************************************************         
                                     24 *                                                                               
                                     25 *        @PGM       PROGRAM CONTROL SECTION                                     
                                     26 *                                                                               
                                     27 ***********************************************************************         
                                                                                                                        
000000                               29 @PGM     CSECT                          PROGRAM ENTRY                           
                                                                                                                        
                            00000    31          USING @PGM,R12                                                         
                                                                                                                        
000000 90EC D00C      0000C          33          STM   R14,R12,12(R13)          SAVE CALLER REGS                        
000004 18CF                          34          LR    R12,R15                  @PGM BASE                               
                                                                                                                        
                                     36          WTO   'HELLO, WORLD!'          NOTIFY CONSOLE                          
000006 0700                          37+         CNOP  0,4                                                      09800002
000008 4510 C01E      0001E          38+         BAL   1,IHB0001A                        BRANCH AROUND MESSAGE  09850002
00000C 0011                          39+         DC    AL2(17)             TEXT LENGTH                          13200002
00000E 0000                          40+         DC    B'0000000000000000' MCS FLAGS                            13250002
000010 C8C5D3D3D66B40E6              41+         DC    C'HELLO, WORLD!'                                         13350002
00001E                               42+IHB0001A DS    0H                                                       15850002
00001E 0A23                          43+         SVC   35                                                       15950002
                                                                                                                        
000020 17FF                          45          XR    R15,R15                  ZERO RESULT CODE                        
                                                                                                                        
000022 980C D014      00014          47          LM    R0,R12,20(R13)           LOAD CALLER REGS                        
000026 58E0 D00C      0000C          48          L     R14,12(,R13)             LOAD RETURN ADDR                        
00002A 07FE                          49          BR    R14                      RETURN TO CALLER                        
                                                                                                                        
                                     51          DROP  R12                                                              
                                                                                                               PAGE    4
  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 17.53 12/17/20
                                     54 ***********************************************************************         
                                     55 *                                                                               
                                     56 *        W O R K I N G   S T O R A G E                                          
                                     57 *                                                                               
                                     58 ***********************************************************************         
                                                                                                                        
                                     60          COPY  EREGS                                                            
                                     61 ***********************************************************************         
                                     62 *                                                                               
                                     63 *        EQUATES                                                                
                                     64 *                                                                               
                                     65 ***********************************************************************         
                                                                                                                        
                                     67 *                                       *******************************         
                                     68 *                                       *  REGISTERS                            
                                     69 *                                       *******************************         
                                                                                                                        
                            00000    71 R0       EQU   0                                                                
                            00001    72 R1       EQU   1                                                                
                            00002    73 R2       EQU   2                                                                
                            00003    74 R3       EQU   3                                                                
                            00004    75 R4       EQU   4                                                                
                            00005    76 R5       EQU   5                                                                
                            00006    77 R6       EQU   6                                                                
                            00007    78 R7       EQU   7                                                                
                            00008    79 R8       EQU   8                                                                
                            00009    80 R9       EQU   9                                                                
                            0000A    81 R10      EQU   10                                                               
                            0000B    82 R11      EQU   11                                                               
                            0000C    83 R12      EQU   12                                                               
                            0000D    84 R13      EQU   13                                                               
                            0000E    85 R14      EQU   14                                                               
                            0000F    86 R15      EQU   15                                                               
                                                                                                               PAGE    5
  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 17.53 12/17/20
000030                               88          LTORG                                                                  
000000                               89          END   @PGM                     END OF PROGRAM                          
                                                  CROSS-REFERENCE                                              PAGE    6
SYMBOL    LEN   VALUE   DEFN    REFERENCES                                                       ASM 0201 17.53 12/17/20
@PGM     00001 00000000 00029  00031 00089                                                                              
IHB0001A 00002 0000001E 00042  00038                                                                                    
R0       00001 00000000 00071  00047                                                                                    
R1       00001 00000001 00072                                                                                           
R10      00001 0000000A 00081                                                                                           
R11      00001 0000000B 00082                                                                                           
R12      00001 0000000C 00083  00031 00033 00034 00047 00051                                                            
R13      00001 0000000D 00084  00033 00047 00048                                                                        
R14      00001 0000000E 00085  00033 00048 00049                                                                        
R15      00001 0000000F 00086  00034 00045 00045                                                                        
R2       00001 00000002 00073                                                                                           
R3       00001 00000003 00074                                                                                           
R4       00001 00000004 00075                                                                                           
R5       00001 00000005 00076                                                                                           
R6       00001 00000006 00077                                                                                           
R7       00001 00000007 00078                                                                                           
R8       00001 00000008 00079                                                                                           
R9       00001 00000009 00080                                                                                           
                                                  ASSEMBLER DIAGNOSTICS AND STATISTICS                         PAGE    7
                                                                                                 ASM 0201 17.53 12/17/20
NO STATEMENTS FLAGGED IN THIS ASSEMBLY                                                                                  
HIGHEST SEVERITY WAS    0                                                                                               
OPTIONS FOR THIS ASSEMBLY                                                                                               
  ALIGN, ALOGIC, BUFSIZE(STD), DECK, ESD, FLAG(0), LINECOUNT(55), LIST, NOMCALL, YFLAG, WORKSIZE(2097152)               
  NOMLOGIC, NONUMBER, OBJECT, RENT, RLD, NOSTMT, NOLIBMAC, NOTERMINAL, NOTEST, XREF                                     
  SYSPARM()                                                                                                             
WORK FILE BUFFER SIZE/NUMBER =19066/ 1                                                                                  
TOTAL RECORDS READ FROM SYSTEM INPUT         56                                                                         
TOTAL RECORDS READ FROM SYSTEM LIBRARY     1090                                                                         
TOTAL RECORDS PUNCHED                         4                                                                         
TOTAL RECORDS PRINTED                       131                                                                         
```

### Linker Messages

The linker discovers one CSECT, @PGM, and the program can be safely reentered and reused.
```
 F64-LEVEL LINKAGE EDITOR OPTIONS SPECIFIED XREF,LET,LIST,NCAL,RENT                                                    
          DEFAULT OPTION(S) USED -  SIZE=(229376,55296)                                                                
                                                                                                                       
                                                CROSS REFERENCE TABLE                                                  
                                                                                                                       
  CONTROL SECTION                       ENTRY                                                                          
    NAME    ORIGIN  LENGTH                NAME   LOCATION     NAME   LOCATION     NAME   LOCATION     NAME   LOCATION  
  @PGM          00      30                                                                                             
                                                                                                                       
                                                                                                                       
 ENTRY ADDRESS       00                                                                                                
 TOTAL LENGTH        30                                                                                                
****ASM002    NOW REPLACED IN DATA SET                                                                                 
AUTHORIZATION CODE IS         0.                                                                                       
**MODULE HAS BEEN MARKED REENTERABLE, AND REUSABLE.                                                                    
```

### Console Output

ASM002 produces the same console message as ASM001.
```
17.53.55 JOB  149  $HASP100 ASM002   ON INTRDR      DWALL01
17.53.55 JOB  149  $HASP373 ASM002   STARTED - INIT  1 - CLASS A - SYS HMVS
17.53.55 JOB  149  IEF403I ASM002 - STARTED - TIME=17.53.55
17.53.56 JOB  149  IEFACTRT JS010   /IFOX00  /00:00:00.78/00:00:01.42/00000/ASM002
17.53.57 JOB  149  IEFACTRT JS020   /IEWL    /00:00:00.16/00:00:00.38/00000/ASM002
17.53.57 JOB  149  +HELLO, WORLD!
17.53.57 JOB  149  IEFACTRT JS030   /ASM002  /00:00:00.04/00:00:00.16/00000/ASM002
17.53.57 JOB  149  IEF404I ASM002 - ENDED - TIME=17.53.57
17.53.57 JOB  149  $HASP395 ASM002   ENDED
17.53.57           $HASP309    INIT  1 INACTIVE ******** C=A
17.53.57 JOB  149  $HASP150 ASM002   ON PUNCH1           4 CARDS
17.53.57 JOB  149  $HASP150 ASM002   ON PRINTER1       158 LINES
17.53.57           $HASP160 PUNCH1   INACTIVE - CLASS=B
17.53.57           $HASP160 PRINTER1 INACTIVE - CLASS=A
17.53.57 JOB  149  $HASP250 ASM002   IS PURGED
```

<hr>

Return to [Sample Programs](Samples.md)  
Return to [README](../README.md)
