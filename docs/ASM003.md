## Sample Program ASM003

### Source

DWALL01.DEV.SORLIB(ASM003) sends a message to the operator console.

This program builds on the previous two. We will now have variables that need a data section (DSECT) to hold them. In fact, the DSECT only defines the layout of storage. In order for our program to really have storage it can modify, it must allocate it, similarly to how "malloc" works in C programs. The calling program's save area is not an exception to this rule. The storage given to us at register 13 was allocated too, but by the calling process. Storage allocation is provided by the GETMAIN macro and released by the FREEMAIN macro. This program also introduces subroutines that are called using the branch and link (BAL) instruction. We define an "initialization" subroutine (@INIT) that will perform some setup steps, a "mainline" subroutine (@MAIN) that will contain our program's main logic lopp, and a "finalize" subroutine (@FINAL) that will perform any step required to clean up before the program exits. Each routine has its own "flower box" documentation that describes the subroutine's purpose and its entry and exit register values or usage. In this program, @INIT and @FINAL don't do anything except set register 15, our result code, to zero. Our "WTO" macro is called in @MAIN. By convention, a subroutine should save and restore the content of any register it modifies, except for its return value in R15. We defined variables explicitly for saving return addresses. This can help when debugging a program because inspecting these variables shows whether a subroutine was called.

By the way, I name my routines beginning with an "at" sign (@) only to make them easy to group and find in the assembler listing. There is no rule about routine names except that they be unique.

Our program begins like ASM002. But after the calling program's registers are saved and our base register is loaded, we call GETMAIN to request just enough storage to fit all our variables defined in the $PGM DSECT. The equate E$PGML is a computed value that measures the size of the DSECT. That value is passed as a parameter to GETMAIN in R0. The address of the allocated storate is returned in R1. Our call to GETMAIN is "unconditional". If the system is not able to allocate the storage requested, the program will terminate.

After obtaining storage, we clear the storage to nulls using the MVCL instruction, which performs a null fill if the 2nd register pair are both zero. We'll use the allocated storage also as our own program's save area. This requires several specific steps. First we save our caller's save area address (R13) in our new save area. Next we save the address of our new save area (R1) in our caller's save area. This creates a link between save areas. Now we can load R13 with the address of our allocated storage. With that, we can call subprograms or system services that require a save area without further modifying our caller's save area, where we stored our registers on entry.

Next, @PGM calls the @INIT subroutine and checks R15 upon return to see if @INIT succeeded. If it did, we call @MAIN. In any case, we then call @FINAL. Once our program's purpose is completed, we now need to release our allocated storage, restore our caller's registers and return. We need to take care with R15 here, though. R15 is our own programs return code, which should represent our program's final result. But, FREEMAIN alters R15. So before we call it, we save R15 in R2. After FREEMAIN, we'll restore R15 from R2. FREEMAIN takes both the address and the size of the allocated storage as parameters. We also have to remember to restore our caller's save area address out of the storage we're about to release. Now we can exit in the same way ASM002 did, by restoring our caller's register values and branching to R14.

One more thing. Did you notice that we have a second "USING" and a second "DROP" statement? Just as we needed a base register for our CSECT, we also need one for our DSECT, so when we reference a field in the DSECT, the assembler knows what base register to use to construct the base + displacement address operand. Since our storage is also our save area at R13, we are "USING" R13 as the base address for the DSECT $PGM.
```
***********************************************************************
*
*        PROGRAM    ASM003
*
*        THIS SAMPLE PROGRAM ILLUSTRATES THE USE OF A DATA SECTION
*        (DSECT) TO DEFINE THE LAYOUT OF STORAGE IN AN ALLOCATED
*        MEMORY SPACE, ADDRESSED RELATIVE TO A BASE REGISTER. GETMAIN
*        AND FREEMAIN MACROS ARE INVOKED TO ALLOCATE AND RELEASE MAIN
*        MEMORY. SUBROUTINES TO INITIALIZE AND FINALIZE THE PROGRAM
*        AND A MAINLINE SUBROUTINE TO HOLD OUR PRIMARY LOGIC LOOP ARE
*        DEFINED TO MAKE THE PROGRAM MORE MODULAR.
*
*        ENTRY      R1      PROGRAM ARGUMENTS ADDRESS
*                   R13     CALLER SAVE AREA ADDRESS
*                   R14     RETURN ADDRESS
*                   R15     CONTROL SECTION ADDRESS
*
*        EXIT       R14     RETURN ADDRESS
*                   R15     RESULT CODE
*
***********************************************************************
         EJECT
***********************************************************************
*
*        @PGM       PROGRAM CONTROL SECTION
*
***********************************************************************
         SPACE
@PGM     CSECT                          PROGRAM ENTRY
         SPACE
         USING @PGM,R12
         USING $PGM,R13
         SPACE
         STM   R14,R12,12(R13)          SAVE CALLER REGS
         LR    R12,R15                  @PGM BASE
         SPACE
         LA    R0,E$PGML                LENGTH OF STORAGE
         SPACE
         GETMAIN R,LV=(0)               GET STORAGE ADDR IN REG 1
         SPACE
         LR    R14,R1                   $PGM ADDR
         LA    R15,E$PGML               $PGM SIZE
         XR    R2,R2                    SOURCE ADDR (NO COPY)
         LR    R3,R2                    SOURCE LENGTH (PAD)
         MVCL  R14,R2                   CLEAR STORAGE TO ZEROS
         SPACE
         ST    R13,4(,R1)               SAVE PARENT SAVE AREA ADDR
         ST    R1,8(,R13)               SAVE CURRENT SAVE AREA ADDR
         LR    R13,R1                   LOAD SAVE AREA ADDR
         SPACE
         BAL   R14,@INIT                INITIALIZE PROGRAM
         SPACE
         LTR   R15,R15                  IF INITIALIZATION SUCCEEDED
         BNZ   @PGM10
         SPACE
         BAL   R14,@MAIN                  DO MAINLINE LOGIC
         SPACE
@PGM10   DS    0H                       END IF
         SPACE
         BAL   R14,@FINAL               FINALIZE PROGRAM
         SPACE
         LR    R2,R15                   HOLD RESULT CODE
         SPACE
         LA    R0,E$PGML                LENGTH OF STORAGE
         LR    R1,R13                   SAVE AREA ADDR
         L     R13,4(,R1)               RESTORE PARENT SAVEAREA ADDR
         SPACE
         FREEMAIN R,LV=(R0),A=(R1)      RELEASE STORAGE
         SPACE
         LR    R15,R2                   RESULT CODE
         SPACE
         LM    R0,R12,20(R13)           LOAD CALLER REGS
         L     R14,12(,R13)             LOAD RETURN ADDR
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R12,R13
         EJECT
***********************************************************************
*
*        @INIT      INITIALIZE PROGRAM
*
***********************************************************************
         SPACE
@INIT    DS    0H
         SPACE
         USING @PGM,R12
         USING $PGM,R13
         SPACE
         ST    R14,PGM$I                SAVE RETURN ADDR (,13)
         SPACE
         XR    R15,R15                  ZERO RESULT CODE
         SPACE
         L     R14,PGM$I                LOAD RETURN ADDR (,13)
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R12,R13
         EJECT
***********************************************************************
*
*        @MAIN      MAIN LOGIC LOOP
*
***********************************************************************
         SPACE
@MAIN    DS    0H
         SPACE
         USING @PGM,R12
         USING $PGM,R13
         SPACE
         ST    R14,PGM$M                SAVE RETURN ADDR
         SPACE
         WTO   'HELLO, WORLD!'          NOTIFY CONSOLE
         SPACE
         L     R14,PGM$M                LOAD RETURN ADDR
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R12,R13
         EJECT
***********************************************************************
*
*        @FINAL     FINALIZATION ROUTINE
*
***********************************************************************
         SPACE
@FINAL   DS    0H
         SPACE
         USING @PGM,R12
         USING $PGM,R13
         SPACE
         ST    R14,PGM$F                SAVE RETURN ADDR
         SPACE
         XR    R15,R15                  ZERO RESULT CODE
         SPACE
         L     R14,PGM$F                LOAD RETURN ADDR
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R12,R13
         EJECT
***********************************************************************
*
*        W O R K I N G   S T O R A G E
*
***********************************************************************
         SPACE
         COPY  EREGS
         EJECT
         LTORG
         EJECT
***********************************************************************
*
*        $PGM       PROGRAM DATA SECTION
*
***********************************************************************
         SPACE
$PGM     DSECT
         SPACE
         DS    18F                      REGISTER SAVE AREA
         SPACE
PGM$I    DS    F                        @INIT RETURN ADDR
PGM$M    DS    F                        @MAIN RETURN ADDR
PGM$F    DS    F                        @FINAL RETURN ADDR
         SPACE
E$PGML   EQU   *-$PGM                   SIZE OF SAVE AREA
         SPACE
         EJECT
         END   @PGM                     END OF PROGRAM
```

### Job Control Language

DWALL01.DEV.JCLLIB(ASM003) assembles, links and runs the program.

JS010 runs the IFOX00 program to assemble the source code into an object deck.  
JS020 runs the IEWL program to read the object deck and produce an executable load module.  
JS030 runs the load module.
```
//ASM003   JOB (001),'DWALL01',CLASS=A,MSGCLASS=A,
//             MSGLEVEL=(1,1),NOTIFY=DWALL01
//*
//*            ASSEMBLE THE PROGRAM
//*
//JS010   EXEC PGM=IFOX00,REGION=256K,
//             PARM='OBJ,RENT,XREF'
//SYSLIB    DD DISP=SHR,DSN=SYS1.MACLIB
//          DD DISP=SHR,DSN=DWALL01.DEV.MACLIB
//SYSUT1    DD DSN=&&SYSUT1,UNIT=SYSSQ,
//             SPACE=(1700,(600,100)),SEP=(SYSLIB)
//SYSUT2    DD DSN=&&SYSUT2,UNIT=SYSSQ,
//             SPACE=(1700,(300,50)),SEP=(SYSUT1)
//SYSUT3    DD DSN=&&SYSUT3,UNIT=SYSSQ,
//             SPACE=(1700,(300,50))
//SYSPRINT  DD DISP=SHR,DSN=DWALL01.DEV.SYSPRINT(IFOX00)
//SYSPUNCH  DD SYSOUT=B
//SYSGO     DD DISP=SHR,DSN=DWALL01.DEV.OBJLIB(ASM003)
//SYSIN     DD DISP=SHR,DSN=DWALL01.DEV.SORLIB(ASM003)
//*
//*            LINK THE PROGRAM
//*
//JS020   EXEC PGM=IEWL,REGION=256K,COND=(0,LT),
//             PARM='XREF,LET,LIST,NCAL,RENT'
//SYSUT1    DD DSN=&&SYSUT1,UNIT=(SYSDA,SEP=(SYSLIN,SYSLMOD)),
//             SPACE=(1024,(50,20))
//SYSPRINT  DD DISP=SHR,DSN=DWALL01.DEV.SYSPRINT(IEWL)
//SYSLMOD   DD DISP=SHR,DSN=DWALL01.DEV.MODLIB(ASM003)
//SYSLIN    DD DISP=SHR,DSN=DWALL01.DEV.OBJLIB(ASM003)
//SYSIN     DD *
NAME ASM003(R)
/*
//*
//*            RUN THE PROGRAM
//*
//JS030   EXEC PGM=ASM003,REGION=256K,COND=(0,LT)
//STEPLIB   DD DISP=SHR,DSN=DWALL01.DEV.MODLIB
//SYSPRINT  DD SYSOUT=*
//SYSABEND  DD SYSOUT=*
//SYSUDUMP  DD SYSOUT=*
//
```

### Assembly Listing

No literals were used in the program, so the literal pool (LTORG) is empty.
```
1                                               EXTERNAL SYMBOL DICTIONARY                                      PAGE    1
-SYMBOL   TYPE  ID   ADDR  LENGTH LDID                                                            ASM 0201 18.11 12/17/20
0@PGM      SD  0001 000000 000098
1                                                                                                               PAGE    2
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 18.11 12/17/20
0                                      1 ***********************************************************************
                                       2 *
                                       3 *        PROGRAM    ASM003
                                       4 *
                                       5 *        THIS SAMPLE PROGRAM ILLUSTRATES THE USE OF A DATA SECTION
                                       6 *        (DSECT) TO DEFINE THE LAYOUT OF STORAGE IN AN ALLOCATED
                                       7 *        MEMORY SPACE, ADDRESSED RELATIVE TO A BASE REGISTER. GETMAIN
                                       8 *        AND FREEMAIN MACROS ARE INVOKED TO ALLOCATE AND RELEASE MAIN
                                       9 *        MEMORY. SUBROUTINES TO INITIALIZE AND FINALIZE THE PROGRAM
                                      10 *        AND A MAINLINE SUBROUTINE TO HOLD OUR PRIMARY LOGIC LOOP ARE
                                      11 *        DEFINED TO MAKE THE PROGRAM MORE MODULAR.
                                      12 *
                                      13 *        ENTRY      R1      PROGRAM ARGUMENTS ADDRESS
                                      14 *                   R13     CALLER SAVE AREA ADDRESS
                                      15 *                   R14     RETURN ADDRESS
                                      16 *                   R15     CONTROL SECTION ADDRESS
                                      17 *
                                      18 *        EXIT       R14     RETURN ADDRESS
                                      19 *                   R15     RESULT CODE
                                      20 *
                                      21 ***********************************************************************
1                                                                                                               PAGE    3
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 18.11 12/17/20
0                                     23 ***********************************************************************
                                      24 *
                                      25 *        @PGM       PROGRAM CONTROL SECTION
                                      26 *
                                      27 ***********************************************************************

 000000                               29 @PGM     CSECT                          PROGRAM ENTRY

                             00000    31          USING @PGM,R12
                             00000    32          USING $PGM,R13

 000000 90EC D00C      0000C          34          STM   R14,R12,12(R13)          SAVE CALLER REGS
 000004 18CF                          35          LR    R12,R15                  @PGM BASE

 000006 4100 0054      00054          37          LA    R0,E$PGML                LENGTH OF STORAGE

                                      39          GETMAIN R,LV=(0)               GET STORAGE ADDR IN REG 1
 00000A 4510 C00E      0000E          40+         BAL   1,*+4                             INDICATE GETMAIN
 00000E 0A0A                          41+         SVC   10                                ISSUE GETMAIN SVC

 000010 18E1                          43          LR    R14,R1                   $PGM ADDR
 000012 41F0 0054      00054          44          LA    R15,E$PGML               $PGM SIZE
 000016 1722                          45          XR    R2,R2                    SOURCE ADDR (NO COPY)
 000018 1832                          46          LR    R3,R2                    SOURCE LENGTH (PAD)
 00001A 0EE2                          47          MVCL  R14,R2                   CLEAR STORAGE TO ZEROS

 00001C 50D0 1004      00004          49          ST    R13,4(,R1)               SAVE PARENT SAVE AREA ADDR
 000020 5010 D008      00008          50          ST    R1,8(,R13)               SAVE CURRENT SAVE AREA ADDR
 000024 18D1                          51          LR    R13,R1                   LOAD SAVE AREA ADDR

 000026 45E0 C058      00058          53          BAL   R14,@INIT                INITIALIZE PROGRAM

 00002A 12FF                          55          LTR   R15,R15                  IF INITIALIZATION SUCCEEDED
 00002C 4770 C034      00034          56          BNZ   @PGM10

 000030 45E0 C064      00064          58          BAL   R14,@MAIN                  DO MAINLINE LOGIC

 000034                               60 @PGM10   DS    0H                       END IF

 000034 45E0 C086      00086          62          BAL   R14,@FINAL               FINALIZE PROGRAM

 000038 182F                          64          LR    R2,R15                   HOLD RESULT CODE

 00003A 4100 0054      00054          66          LA    R0,E$PGML                LENGTH OF STORAGE
 00003E 181D                          67          LR    R1,R13                   SAVE AREA ADDR
 000040 58D0 1004      00004          68          L     R13,4(,R1)               RESTORE PARENT SAVEAREA ADDR

                                      70          FREEMAIN R,LV=(R0),A=(R1)      RELEASE STORAGE
                                      71+*      OS/VS2 RELEASE 3 VERSION  -- 10/25/74                            00001603
 000044 1800                          72+         LR    0,R0                              LOAD LENGTH            00192003
 000046 4110 1000      00000          73+         LA    1,0(0,R1)                         LOAD AREA ADDRESS      00164002
 00004A 0A0A                          74+         SVC   10                                ISSUE FREEMAIN SVC     00311202

 00004C 18F2                          76          LR    R15,R2                   RESULT CODE

1                                                                                                               PAGE    4
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 18.11 12/17/20
000004E 980C D014      00014          78          LM    R0,R12,20(R13)           LOAD CALLER REGS
 000052 58E0 D00C      0000C          79          L     R14,12(,R13)             LOAD RETURN ADDR
 000056 07FE                          80          BR    R14                      RETURN TO CALLER

                                      82          DROP  R12,R13
1                                                                                                               PAGE    5
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 18.11 12/17/20
0                                     84 ***********************************************************************
                                      85 *
                                      86 *        @INIT      INITIALIZE PROGRAM
                                      87 *
                                      88 ***********************************************************************

 000058                               90 @INIT    DS    0H

                             00000    92          USING @PGM,R12
                             00000    93          USING $PGM,R13

 000058 50E0 D048      00048          95          ST    R14,PGM$I                SAVE RETURN ADDR (,13)

 00005C 17FF                          97          XR    R15,R15                  ZERO RESULT CODE

 00005E 58E0 D048      00048          99          L     R14,PGM$I                LOAD RETURN ADDR (,13)
 000062 07FE                         100          BR    R14                      RETURN TO CALLER

                                     102          DROP  R12,R13
1                                                                                                               PAGE    6
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 18.11 12/17/20
0                                    104 ***********************************************************************
                                     105 *
                                     106 *        @MAIN      MAIN LOGIC LOOP
                                     107 *
                                     108 ***********************************************************************

 000064                              110 @MAIN    DS    0H

                             00000   112          USING @PGM,R12
                             00000   113          USING $PGM,R13

 000064 50E0 D04C      0004C         115          ST    R14,PGM$M                SAVE RETURN ADDR

                                     117          WTO   'HELLO, WORLD!'          NOTIFY CONSOLE
 000068                              118+         CNOP  0,4                                                      09800002
 000068 4510 C07E      0007E         119+         BAL   1,IHB0003A                        BRANCH AROUND MESSAGE  09850002
 00006C 0011                         120+         DC    AL2(17)             TEXT LENGTH                          13200002
 00006E 0000                         121+         DC    B'0000000000000000' MCS FLAGS                            13250002
 000070 C8C5D3D3D66B40E6             122+         DC    C'HELLO, WORLD!'                                         13350002
 00007E                              123+IHB0003A DS    0H                                                       15850002
 00007E 0A23                         124+         SVC   35                                                       15950002

 000080 58E0 D04C      0004C         126          L     R14,PGM$M                LOAD RETURN ADDR
 000084 07FE                         127          BR    R14                      RETURN TO CALLER

                                     129          DROP  R12,R13
1                                                                                                               PAGE    7
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 18.11 12/17/20
0                                    131 ***********************************************************************
                                     132 *
                                     133 *        @FINAL     FINALIZATION ROUTINE
                                     134 *
                                     135 ***********************************************************************

 000086                              137 @FINAL   DS    0H

                             00000   139          USING @PGM,R12
                             00000   140          USING $PGM,R13

 000086 50E0 D050      00050         142          ST    R14,PGM$F                SAVE RETURN ADDR

 00008A 17FF                         144          XR    R15,R15                  ZERO RESULT CODE

 00008C 58E0 D050      00050         146          L     R14,PGM$F                LOAD RETURN ADDR
 000090 07FE                         147          BR    R14                      RETURN TO CALLER

                                     149          DROP  R12,R13
1                                                                                                               PAGE    8
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 18.11 12/17/20
0                                    151 ***********************************************************************
                                     152 *
                                     153 *        W O R K I N G   S T O R A G E
                                     154 *
                                     155 ***********************************************************************

                                     157          COPY  EREGS
                                     158 ***********************************************************************
                                     159 *
                                     160 *        EQUATES
                                     161 *
                                     162 ***********************************************************************

                                     164 *                                       *******************************
                                     165 *                                       *  REGISTERS
                                     166 *                                       *******************************

                             00000   168 R0       EQU   0
                             00001   169 R1       EQU   1
                             00002   170 R2       EQU   2
                             00003   171 R3       EQU   3
                             00004   172 R4       EQU   4
                             00005   173 R5       EQU   5
                             00006   174 R6       EQU   6
                             00007   175 R7       EQU   7
                             00008   176 R8       EQU   8
                             00009   177 R9       EQU   9
                             0000A   178 R10      EQU   10
                             0000B   179 R11      EQU   11
                             0000C   180 R12      EQU   12
                             0000D   181 R13      EQU   13
                             0000E   182 R14      EQU   14
                             0000F   183 R15      EQU   15
1                                                                                                               PAGE    9
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 18.11 12/17/20
0000098                              185          LTORG
1                                                                                                               PAGE   10
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 18.11 12/17/20
0                                    187 ***********************************************************************
                                     188 *
                                     189 *        $PGM       PROGRAM DATA SECTION
                                     190 *
                                     191 ***********************************************************************

 000000                              193 $PGM     DSECT

 000000                              195          DS    18F                      REGISTER SAVE AREA

 000048                              197 PGM$I    DS    F                        @INIT RETURN ADDR
 00004C                              198 PGM$M    DS    F                        @MAIN RETURN ADDR
 000050                              199 PGM$F    DS    F                        @FINAL RETURN ADDR

                             00054   201 E$PGML   EQU   *-$PGM                   SIZE OF SAVE AREA

1                                                                                                               PAGE   11
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 18.11 12/17/20
0000000                              204          END   @PGM                     END OF PROGRAM
1                                                  CROSS-REFERENCE                                              PAGE   12
-SYMBOL    LEN   VALUE   DEFN    REFERENCES                                                       ASM 0201 18.11 12/17/20
0$PGM     00001 00000000 00193  00032 00093 00113 00140 00201
 @FINAL   00002 00000086 00137  00062
 @INIT    00002 00000058 00090  00053
 @MAIN    00002 00000064 00110  00058
 @PGM     00001 00000000 00029  00031 00092 00112 00139 00204
 @PGM10   00002 00000034 00060  00056
 E$PGML   00001 00000054 00201  00037 00044 00066
 IHB0003A 00002 0000007E 00123  00119
 PGM$F    00004 00000050 00199  00142 00146
 PGM$I    00004 00000048 00197  00095 00099
 PGM$M    00004 0000004C 00198  00115 00126
 R0       00001 00000000 00168  00037 00066 00072 00078
 R1       00001 00000001 00169  00043 00049 00050 00051 00067 00068 00073
 R10      00001 0000000A 00178
 R11      00001 0000000B 00179
 R12      00001 0000000C 00180  00031 00034 00035 00078 00082 00092 00102 00112 00129 00139 00149
 R13      00001 0000000D 00181  00032 00034 00049 00050 00051 00067 00068 00078 00079 00082 00093 00102 00113 00129 00140
                                00149
 R14      00001 0000000E 00182  00034 00043 00047 00053 00058 00062 00079 00080 00095 00099 00100 00115 00126 00127 00142
                                00146 00147
 R15      00001 0000000F 00183  00035 00044 00055 00055 00064 00076 00097 00097 00144 00144
 R2       00001 00000002 00170  00045 00045 00046 00047 00064 00076
 R3       00001 00000003 00171  00046
 R4       00001 00000004 00172
 R5       00001 00000005 00173
 R6       00001 00000006 00174
 R7       00001 00000007 00175
 R8       00001 00000008 00176
 R9       00001 00000009 00177
1                                                  ASSEMBLER DIAGNOSTICS AND STATISTICS                         PAGE   13
-                                                                                                 ASM 0201 18.11 12/17/20
0NO STATEMENTS FLAGGED IN THIS ASSEMBLY
 HIGHEST SEVERITY WAS    0
 OPTIONS FOR THIS ASSEMBLY
   ALIGN, ALOGIC, BUFSIZE(STD), DECK, ESD, FLAG(0), LINECOUNT(55), LIST, NOMCALL, YFLAG, WORKSIZE(2097152)
   NOMLOGIC, NONUMBER, OBJECT, RENT, RLD, NOSTMT, NOLIBMAC, NOTERMINAL, NOTEST, XREF
   SYSPARM()
 WORK FILE BUFFER SIZE/NUMBER =19066/ 1
 TOTAL RECORDS READ FROM SYSTEM INPUT        165
 TOTAL RECORDS READ FROM SYSTEM LIBRARY     1889
 TOTAL RECORDS PUNCHED                         6
 TOTAL RECORDS PRINTED                       263
```

### Linker Messages

The linker finds one control section. The program is reentrant and reusable.
```                                                                                                                      
 F64-LEVEL LINKAGE EDITOR OPTIONS SPECIFIED XREF,LET,LIST,NCAL,RENT                                                  
          DEFAULT OPTION(S) USED -  SIZE=(229376,55296)                                                              
                                                                                                                     
                                                CROSS REFERENCE TABLE                                                
                                                                                                                     
  CONTROL SECTION                       ENTRY                                                                        
    NAME    ORIGIN  LENGTH                NAME   LOCATION     NAME   LOCATION     NAME   LOCATION     NAME   LOCATION
  @PGM          00      98                                                                                           
                                                                                                                     
                                                                                                                     
 ENTRY ADDRESS       00                                                                                              
 TOTAL LENGTH        98                                                                                              
****ASM003    NOW REPLACED IN DATA SET                                                                               
AUTHORIZATION CODE IS         0.                                                                                     
**MODULE HAS BEEN MARKED REENTERABLE, AND REUSABLE.                                                                  
```

### Console Output

The WTO output, "HELLO, WORLD!", appears as usual in the console output visible in the Hercules web console.
```
18.11.32 JOB  150  $HASP100 ASM003   ON INTRDR      DWALL01
18.11.32 JOB  150  $HASP373 ASM003   STARTED - INIT  1 - CLASS A - SYS HMVS
18.11.32 JOB  150  IEF403I ASM003 - STARTED - TIME=18.11.32
18.11.34 JOB  150  IEFACTRT JS010   /IFOX00  /00:00:01.10/00:00:01.76/00000/ASM003
18.11.34 JOB  150  IEFACTRT JS020   /IEWL    /00:00:00.12/00:00:00.37/00000/ASM003
18.11.34 JOB  150  +HELLO, WORLD!
18.11.34 JOB  150  IEFACTRT JS030   /ASM003  /00:00:00.04/00:00:00.17/00000/ASM003
18.11.34 JOB  150  IEF404I ASM003 - ENDED - TIME=18.11.34
18.11.34 JOB  150  $HASP395 ASM003   ENDED
18.11.34           $HASP309    INIT  1 INACTIVE ******** C=A
18.11.34 JOB  150  $HASP150 ASM003   ON PUNCH1           6 CARDS
18.11.34 JOB  150  $HASP150 ASM003   ON PRINTER1       158 LINES
18.11.34           $HASP160 PUNCH1   INACTIVE - CLASS=B
18.11.34           $HASP160 PRINTER1 INACTIVE - CLASS=A
18.11.34 JOB  150  $HASP250 ASM003   IS PURGED
```

<hr>

Return to [Sample Programs](Samples.md)  
Return to [README](../README.md)
