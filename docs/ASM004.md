## Sample Program ASM004

### Source

DWALL01.DEV.SORLIB(ASM004) writes a record to a new GDG generation.

In this program we will write one record to an output file which is a new GDG generation. There is JCL in the jcllib folder as part of this project and in DWALL01.DEV.JCLLIB(GDG) that recreates a Generation Data Group (GDG) base catalog definition and model data set. With those built, we can create a new data set "generation", such as "DWALL01.GDG.LOG.G0001V00" when we run our program. Each time we run the program a new generation is created with an incremented generation number.

Writing to this data set involves declaring a Data Control Block (DCB) in our CSECT and invoking OPEN, WRITE and CLOSE macros to perform those operations. When we write a record to the data set, we reuse the same record buffer. Take care to clear the buffer or pad it with spaces or nulls so that shorter message do not also have the trailing content of earlier longer messages.

We write up to three operator console messages in this program. The first is if the output data set is opened. The second is after the record is written. The third is when or if the data set is closed.
```
***********************************************************************
*
*        PROGRAM    ASM004
*
*        THIS SAMPLE PROGRAM WRITES A MESSAGE TO A DATA SET. A DATA
*        CONTROL BLOCK (DCB) IS DEFINED IN THE WORKING STORAGE SECTION
*        THAT CORRESPONDS TO A DATA DEFINTION (DD) STATEMENT IN THE
*        JOB THAT RUNS THE PROGRAM. STANDARD OPEN, PUT AND CLOSE MACROS
*        ARE CALLED TO PEROFRM THE I/O.
*
*        ENTRY      R1      PROGRAM ARGUMENTS ADDRESS
*                   R13     CALLER SAVE AREA ADDRESS
*                   R14     RETURN ADDRESS
*                   R15     CONTROL SECTION ADDRESS
*
*        EXIT       R14     RETURN ADDRESS
*                   R15     RESULT CODE
*
***********************************************************************
         EJECT
***********************************************************************
*
*        @PGM       PROGRAM CONTROL SECTION
*
***********************************************************************
         SPACE
@PGM     CSECT                          PROGRAM ENTRY
         SPACE
         USING @PGM,R12
         USING $PGM,R13
         SPACE
         STM   R14,R12,12(R13)          SAVE CALLER REGS
         LR    R12,R15                  @PGM BASE
         SPACE
         LA    R0,E$PGML                LENGTH OF STORAGE
         SPACE
         GETMAIN R,LV=(0)               GET STORAGE ADDR IN REG 1
         SPACE
         LR    R14,R1                   $PGM ADDR
         LA    R15,E$PGML               $PGM SIZE
         XR    R2,R2                    SOURCE ADDR (NO COPY)
         LR    R3,R2                    SOURCE LENGTH (PAD)
         MVCL  R14,R2                   CLEAR STORAGE TO ZEROS
         SPACE
         ST    R13,4(,R1)               SAVE PARENT SAVE AREA ADDR
         ST    R1,8(,R13)               SAVE CURRENT SAVE AREA ADDR
         LR    R13,R1                   LOAD SAVE AREA ADDR
         SPACE
         BAL   R14,@INIT                INITIALIZE PROGRAM
         SPACE
         LTR   R15,R15                  IF INITIALIZATION SUCCEEDED
         BNZ   @PGM10
         SPACE
         BAL   R14,@MAIN                  DO MAINLINE LOGIC
         SPACE
@PGM10   DS    0H                       END IF
         SPACE
         BAL   R14,@FINAL               FINALIZE PROGRAM
         SPACE
         LR    R2,R15                   HOLD RESULT CODE
         SPACE
         LA    R0,E$PGML                LENGTH OF STORAGE
         LR    R1,R13                   SAVE AREA ADDR
         L     R13,4(,R1)               RESTORE PARENT SAVEAREA ADDR
         SPACE
         FREEMAIN R,LV=(R0),A=(R1)      RELEASE STORAGE
         SPACE
         LR    R15,R2                   RESULT CODE
         SPACE
         LM    R0,R12,20(R13)           LOAD CALLER REGS
         L     R14,12(,R13)             LOAD RETURN ADDR
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R12,R13
         EJECT
***********************************************************************
*
*        @INIT      INITIALIZE PROGRAM
*
***********************************************************************
         SPACE
@INIT    DS    0H
         SPACE
         USING @PGM,R12
         USING $PGM,R13
         SPACE
         ST    R14,PGM$I                SAVE RETURN ADDR (,13)
         SPACE
         OPEN  (DCBLOG,(OUTPUT))        OPEN LOG FILE
         SPACE
         LA    R15,ERLOGOPN             ASSUME OPEN FAILED
         TM    DCBLOG+48,X'10'          IF FILE OPENED
         BNO   @INIT10
         SPACE
         WTO   'LOG FILE OPENED'          NOTIFY CONSOLE
         SPACE
         XR    R15,R15                    ZERO RESULT CODE
         SPACE
@INIT10  DS    0H                       END IF
         SPACE
         L     R14,PGM$I                LOAD RETURN ADDR (,13)
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R12,R13
         EJECT
***********************************************************************
*
*        @MAIN      MAIN LOGIC LOOP
*
***********************************************************************
         SPACE
@MAIN    DS    0H
         SPACE
         USING @PGM,R12
         USING $PGM,R13
         SPACE
         ST    R14,PGM$M                SAVE RETURN ADDR
         SPACE
         MVI   WCLOGREC,C' '                       CLEAR RECORD
         MVC   WCLOGREC+1(L'WCLOGREC-1),WCLOGREC   TO SPACES
         MVC   WCLOGREC(L'CCLOGMSG),CCLOGMSG       STORE MESSAGE
         SPACE
         PUT   DCBLOG,WCLOGREC          WRITE LOG RECORD
         SPACE
         WTO   'LOG RECORD WRITTEN'     NOTIFY CONSOLE
         SPACE
@MAIN10  DS    0H                       PUT SYNAD RETURNS HERE
         SPACE
         L     R14,PGM$M                LOAD RETURN ADDR
         BR    R14                      RETURN TO CALLER
         SPACE
@LOGSYN  DS    0H                       PUT SYNAD ENTERS HERE
         SPACE
         LR    R15,R1                   ERROR BITS 0-7
         SRL   R15,24                   ERROR BITS 24-31
         B     @MAIN10                  CONTINUE
         SPACE
         DROP  R12,R13
         EJECT
***********************************************************************
*
*        @FINAL     FINALIZATION ROUTINE
*
***********************************************************************
         SPACE
@FINAL   DS    0H
         SPACE
         USING @PGM,R12
         USING $PGM,R13
         SPACE
         ST    R14,PGM$F                SAVE RETURN ADDR
         SPACE
         TM    DCBLOG+48,X'10'          IF FILE OPENED
         BNO   @FINL10
         SPACE
         CLOSE DCBLOG                     CLOSE LOG FILE
         SPACE
         WTO   'LOG FILE CLOSED'          NOTIFY FILE CLOSED
         SPACE
         XR    R15,R15                    ZERO RESULT CODE
         SPACE
@FINL10  DS    0H                       END IF
         SPACE
         L     R14,PGM$F                LOAD RETURN ADDR
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R12,R13
         EJECT
***********************************************************************
*
*        W O R K I N G   S T O R A G E
*
***********************************************************************
         SPACE
         COPY  EREGS
         SPACE
ERLOGOPN EQU   4                        OPEN FAILED
         SPACE
CCLOGMSG DC    C'LOG MESSAGE'
         SPACE
DCBLOG   DCB   DDNAME=LOG,DSORG=PS,RECFM=FB,                           X
               LRECL=80,MACRF=(PM),SYNAD=@LOGSYN
         EJECT
         LTORG
         EJECT
***********************************************************************
*
*        $PGM       PROGRAM DATA SECTION
*
***********************************************************************
         SPACE
$PGM     DSECT
         SPACE
         DS    18F                      REGISTER SAVE AREA
         SPACE
PGM$I    DS    F                        @INIT RETURN ADDR
PGM$M    DS    F                        @MAIN RETURN ADDR
PGM$F    DS    F                        @FINAL RETURN ADDR
         SPACE
WCLOGREC DS    CL80                     OUTPUT RECORD
         SPACE
E$PGML   EQU   *-$PGM                   SIZE OF SAVE AREA
         SPACE
         EJECT
         END   @PGM                     END OF PROGRAM
```

### Job Control Language

DWALL01.DEV.JCLLIB(ASM004) invokes the procedure SYS2.PROCLIB(ASMLKED) to assemble, link and run the program.

JS010 runs the ASMLKED procedure. The "M" parameter is given the value "ASM004".
``` 
//ASM004   JOB (001),'DWALL01',CLASS=A,MSGCLASS=A,
//             MSGLEVEL=(1,1),NOTIFY=DWALL01
//*
//*            ASSEMBLE, LINK AND RUN
//*
//JS010   EXEC ASMLKED,M=ASM004
//
```

### Procedure

SYS2.PROCLIB(ASMLKED) is the procedure invoked by our JCL.
```
//ASMLKED PROC M=MISSING                                      
//*                                                           
//*            ASSEMBLE THE PROGRAM                           
//*                                                           
//ASM     EXEC PGM=IFOX00,REGION=256K,                        
//             PARM='OBJ,RENT,XREF'                           
//SYSLIB    DD DISP=SHR,DSN=SYS1.MACLIB                       
//          DD DISP=SHR,DSN=DWALL01.DEV.MACLIB                
//SYSUT1    DD DSN=&&SYSUT1,UNIT=SYSSQ,                       
//             SPACE=(1700,(600,100)),SEP=(SYSLIB)            
//SYSUT2    DD DSN=&&SYSUT2,UNIT=SYSSQ,                       
//             SPACE=(1700,(300,50)),SEP=(SYSUT1)             
//SYSUT3    DD DSN=&&SYSUT3,UNIT=SYSSQ,                       
//             SPACE=(1700,(300,50))                          
//SYSPRINT  DD DISP=SHR,DSN=DWALL01.DEV.SYSPRINT(IFOX00)      
//SYSPUNCH  DD SYSOUT=B                                       
//SYSGO     DD DISP=SHR,DSN=DWALL01.DEV.OBJLIB(&M)            
//SYSIN     DD DISP=SHR,DSN=DWALL01.DEV.SORLIB(&M)            
//*                                                           
//*            LINK THE PROGRAM                               
//*                                                           
//LINK    EXEC PGM=IEWL,REGION=256K,COND=(0,LT),              
//             PARM='XREF,LET,LIST,NCAL,RENT'                 
//SYSUT1    DD DSN=&&SYSUT1,UNIT=(SYSDA,SEP=(SYSLIN,SYSLMOD)),
//             SPACE=(1024,(50,20))                           
//SYSPRINT  DD DISP=SHR,DSN=DWALL01.DEV.SYSPRINT(IEWL)        
//SYSLMOD   DD DISP=SHR,DSN=DWALL01.DEV.MODLIB(&M)            
//SYSLIN    DD DISP=SHR,DSN=DWALL01.DEV.OBJLIB(&M)            
//*                                                           
//*            RUN THE PROGRAM                                
//*                                                           
//RUN     EXEC PGM=&M,REGION=256K,COND=(0,LT)                 
//STEPLIB   DD DISP=SHR,DSN=DWALL01.DEV.MODLIB                
//SYSPRINT  DD SYSOUT=*                                       
//SYSABEND  DD SYSOUT=*                                       
//SYSUDUMP  DD SYSOUT=*                                       
//LOG       DD DSN=DWALL01.GDG.LOG(+1),                       
//             DISP=(NEW,CATLG,DELETE),                       
//             UNIT=SYSDA,SPACE=(TRK,(2,1)),                  
//             DCB=DWALL01.GDG.LOGMODEL                       
```

### Assembly Listing

No literals were used in the program, so the literal pool (LTORG) is empty

```
1                                               EXTERNAL SYMBOL DICTIONARY                                      PAGE    1
-SYMBOL   TYPE  ID   ADDR  LENGTH LDID                                                            ASM 0201 19.18 12/17/20
0@PGM      SD  0001 000000 000190
1                                                                                                               PAGE    2
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 19.18 12/17/20
0                                      1 ***********************************************************************
                                       2 *
                                       3 *        PROGRAM    ASM004
                                       4 *
                                       5 *        THIS SAMPLE PROGRAM WRITES A MESSAGE TO A DATA SET. A DATA
                                       6 *        CONTROL BLOCK (DCB) IS DEFINED IN THE WORKING STORAGE SECTION
                                       7 *        THAT CORRESPONDS TO A DATA DEFINTION (DD) STATEMENT IN THE
                                       8 *        JOB THAT RUNS THE PROGRAM. STNDARD OPEN, PUT AND CLOSE MACROS
                                       9 *        ARE CALLED TO PEROFRM THE I/O.
                                      10 *
                                      11 *        ENTRY      R1      PROGRAM ARGUMENTS ADDRESS
                                      12 *                   R13     CALLER SAVE AREA ADDRESS
                                      13 *                   R14     RETURN ADDRESS
                                      14 *                   R15     CONTROL SECTION ADDRESS
                                      15 *
                                      16 *        EXIT       R14     RETURN ADDRESS
                                      17 *                   R15     RESULT CODE
                                      18 *
                                      19 ***********************************************************************
1                                                                                                               PAGE    3
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 19.18 12/17/20
0                                     21 ***********************************************************************
                                      22 *
                                      23 *        @PGM       PROGRAM CONTROL SECTION
                                      24 *
                                      25 ***********************************************************************

 000000                               27 @PGM     CSECT                          PROGRAM ENTRY

                             00000    29          USING @PGM,R12
                             00000    30          USING $PGM,R13

 000000 90EC D00C      0000C          32          STM   R14,R12,12(R13)          SAVE CALLER REGS
 000004 18CF                          33          LR    R12,R15                  @PGM BASE

 000006 4100 00A4      000A4          35          LA    R0,E$PGML                LENGTH OF STORAGE

                                      37          GETMAIN R,LV=(0)               GET STORAGE ADDR IN REG 1
 00000A 4510 C00E      0000E          38+         BAL   1,*+4                             INDICATE GETMAIN
 00000E 0A0A                          39+         SVC   10                                ISSUE GETMAIN SVC

 000010 18E1                          41          LR    R14,R1                   $PGM ADDR
 000012 41F0 00A4      000A4          42          LA    R15,E$PGML               $PGM SIZE
 000016 1722                          43          XR    R2,R2                    SOURCE ADDR (NO COPY)
 000018 1832                          44          LR    R3,R2                    SOURCE LENGTH (PAD)
 00001A 0EE2                          45          MVCL  R14,R2                   CLEAR STORAGE TO ZEROS

 00001C 50D0 1004      00004          47          ST    R13,4(,R1)               SAVE PARENT SAVE AREA ADDR
 000020 5010 D008      00008          48          ST    R1,8(,R13)               SAVE CURRENT SAVE AREA ADDR
 000024 18D1                          49          LR    R13,R1                   LOAD SAVE AREA ADDR

 000026 45E0 C058      00058          51          BAL   R14,@INIT                INITIALIZE PROGRAM

 00002A 12FF                          53          LTR   R15,R15                  IF INITIALIZATION SUCCEEDED
 00002C 4770 C034      00034          54          BNZ   @PGM10

 000030 45E0 C096      00096          56          BAL   R14,@MAIN                  DO MAINLINE LOGIC

 000034                               58 @PGM10   DS    0H                       END IF

 000034 45E0 C0E4      000E4          60          BAL   R14,@FINAL               FINALIZE PROGRAM

 000038 182F                          62          LR    R2,R15                   HOLD RESULT CODE

 00003A 4100 00A4      000A4          64          LA    R0,E$PGML                LENGTH OF STORAGE
 00003E 181D                          65          LR    R1,R13                   SAVE AREA ADDR
 000040 58D0 1004      00004          66          L     R13,4(,R1)               RESTORE PARENT SAVEAREA ADDR

                                      68          FREEMAIN R,LV=(R0),A=(R1)      RELEASE STORAGE
                                      69+*      OS/VS2 RELEASE 3 VERSION  -- 10/25/74                            00001603
 000044 1800                          70+         LR    0,R0                              LOAD LENGTH            00192003
 000046 4110 1000      00000          71+         LA    1,0(0,R1)                         LOAD AREA ADDRESS      00164002
 00004A 0A0A                          72+         SVC   10                                ISSUE FREEMAIN SVC     00311202

 00004C 18F2                          74          LR    R15,R2                   RESULT CODE

1                                                                                                               PAGE    4
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 19.18 12/17/20
000004E 980C D014      00014          76          LM    R0,R12,20(R13)           LOAD CALLER REGS
 000052 58E0 D00C      0000C          77          L     R14,12(,R13)             LOAD RETURN ADDR
 000056 07FE                          78          BR    R14                      RETURN TO CALLER

                                      80          DROP  R12,R13
1                                                                                                               PAGE    5
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 19.18 12/17/20
0                                     82 ***********************************************************************
                                      83 *
                                      84 *        @INIT      INITIALIZE PROGRAM
                                      85 *
                                      86 ***********************************************************************

 000058                               88 @INIT    DS    0H

                             00000    90          USING @PGM,R12
                             00000    91          USING $PGM,R13

 000058 50E0 D048      00048          93          ST    R14,PGM$I                SAVE RETURN ADDR (,13)

                                      95          OPEN  (DCBLOG,(OUTPUT))        OPEN LOG FILE
 00005C                               96+         CNOP  0,4                               ALIGN LIST TO FULLWORD 01740001
 00005C 4510 C064      00064          97+         BAL   1,*+8                             LOAD REG1 W/LIST ADDR. 01780000
 000060 8F                            98+         DC    AL1(143)                          OPTION BYTE            01900000
 000061 00012C                        99+         DC    AL3(DCBLOG)                       DCB ADDRESS            01920000
 000064 0A13                         100+         SVC   19                                ISSUE OPEN SVC         04000000

 000066 41F0 0004      00004         102          LA    R15,ERLOGOPN             ASSUME OPEN FAILED
 00006A 9110 C15C      0015C         103          TM    DCBLOG+48,X'10'          IF FILE OPENED
 00006E 47E0 C090      00090         104          BNO   @INIT10

                                     106          WTO   'LOG FILE OPENED'          NOTIFY CONSOLE
 000072 0700                         107+         CNOP  0,4                                                      09800002
 000074 4510 C08C      0008C         108+         BAL   1,IHB0004A                        BRANCH AROUND MESSAGE  09850002
 000078 0013                         109+         DC    AL2(19)             TEXT LENGTH                          13200002
 00007A 0000                         110+         DC    B'0000000000000000' MCS FLAGS                            13250002
 00007C D3D6C740C6C9D3C5             111+         DC    C'LOG FILE OPENED'                                       13350002
 00008C                              112+IHB0004A DS    0H                                                       15850002
 00008C 0A23                         113+         SVC   35                                                       15950002

 00008E 17FF                         115          XR    R15,R15                    ZERO RESULT CODE

 000090                              117 @INIT10  DS    0H                       END IF

 000090 58E0 D048      00048         119          L     R14,PGM$I                LOAD RETURN ADDR (,13)
 000094 07FE                         120          BR    R14                      RETURN TO CALLER

                                     122          DROP  R12,R13
1                                                                                                               PAGE    6
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 19.18 12/17/20
0                                    124 ***********************************************************************
                                     125 *
                                     126 *        @MAIN      MAIN LOGIC LOOP
                                     127 *
                                     128 ***********************************************************************

 000096                              130 @MAIN    DS    0H

                             00000   132          USING @PGM,R12
                             00000   133          USING $PGM,R13

 000096 50E0 D04C      0004C         135          ST    R14,PGM$M                SAVE RETURN ADDR

 00009A 9240 D054      00054         137          MVI   WCLOGREC,C' '                       CLEAR RECORD
 00009E D24E D055 D054 00055 00054   138          MVC   WCLOGREC+1(L'WCLOGREC-1),WCLOGREC   TO SPACES
 0000A4 D20A D054 C11E 00054 0011E   139          MVC   WCLOGREC(L'CCLOGMSG),CCLOGMSG       STORE MESSAGE

                                     141          PUT   DCBLOG,WCLOGREC          WRITE LOG RECORD
 0000AA 4110 C12C      0012C         142+         LA    1,DCBLOG                          LOAD PARAMETER REG 1   01900002
 0000AE 4100 D054      00054         143+         LA    0,WCLOGREC                        LOAD PARAMETER REG 0   02500002
 0000B2 58F0 1030      00030         144+         L     15,48(0,1)               LOAD PUT ROUTINE ADDR           00550000
 0000B6 05EF                         145+         BALR  14,15                    LINK TO PUT ROUTINE             00600000

                                     147          WTO   'LOG RECORD WRITTEN'     NOTIFY CONSOLE
 0000B8                              148+         CNOP  0,4                                                      09800002
 0000B8 4510 C0D2      000D2         149+         BAL   1,IHB0007A                        BRANCH AROUND MESSAGE  09850002
 0000BC 0016                         150+         DC    AL2(22)             TEXT LENGTH                          13200002
 0000BE 0000                         151+         DC    B'0000000000000000' MCS FLAGS                            13250002
 0000C0 D3D6C740D9C5C3D6             152+         DC    C'LOG RECORD WRITTEN'                                    13350002
 0000D2                              153+IHB0007A DS    0H                                                       15850002
 0000D2 0A23                         154+         SVC   35                                                       15950002

 0000D4                              156 @MAIN10  DS    0H                       PUT SYNAD RETURNS HERE

 0000D4 58E0 D04C      0004C         158          L     R14,PGM$M                LOAD RETURN ADDR
 0000D8 07FE                         159          BR    R14                      RETURN TO CALLER

 0000DA                              161 @LOGSYN  DS    0H                       PUT SYNAD ENTERS HERE

 0000DA 18F1                         163          LR    R15,R1                   ERROR BITS 0-7
 0000DC 88F0 0018      00018         164          SRL   R15,24                   ERROR BITS 24-31
 0000E0 47F0 C0D4      000D4         165          B     @MAIN10                  CONTINUE

                                     167          DROP  R12,R13
1                                                                                                               PAGE    7
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 19.18 12/17/20
0                                    169 ***********************************************************************
                                     170 *
                                     171 *        @FINAL     FINALIZATION ROUTINE
                                     172 *
                                     173 ***********************************************************************

 0000E4                              175 @FINAL   DS    0H

                             00000   177          USING @PGM,R12
                             00000   178          USING $PGM,R13

 0000E4 50E0 D050      00050         180          ST    R14,PGM$F                SAVE RETURN ADDR

 0000E8 9110 C15C      0015C         182          TM    DCBLOG+48,X'10'          IF FILE OPENED
 0000EC 47E0 C118      00118         183          BNO   @FINL10

                                     185          CLOSE DCBLOG                     CLOSE LOG FILE
 0000F0                              186+         CNOP  0,4                               ALIGN LIST TO FULLWORD 02420002
 0000F0 4510 C0F8      000F8         187+         BAL   1,*+8                             LOAD REG1 W/LIST ADDR  02460002
 0000F4 80                           188+         DC    AL1(128)                          OPTION BYTE            02580000
 0000F5 00012C                       189+         DC    AL3(DCBLOG)                       DCB ADDRESS            02600000
 0000F8 0A14                         190+         SVC   20                                ISSUE CLOSE SVC        01640000

                                     192          WTO   'LOG FILE CLOSED'          NOTIFY FILE CLOSED
 0000FA 0700                         193+         CNOP  0,4                                                      09800002
 0000FC 4510 C114      00114         194+         BAL   1,IHB0009A                        BRANCH AROUND MESSAGE  09850002
 000100 0013                         195+         DC    AL2(19)             TEXT LENGTH                          13200002
 000102 0000                         196+         DC    B'0000000000000000' MCS FLAGS                            13250002
 000104 D3D6C740C6C9D3C5             197+         DC    C'LOG FILE CLOSED'                                       13350002
 000114                              198+IHB0009A DS    0H                                                       15850002
 000114 0A23                         199+         SVC   35                                                       15950002

 000116 17FF                         201          XR    R15,R15                    ZERO RESULT CODE

 000118                              203 @FINL10  DS    0H                       END IF

 000118 58E0 D050      00050         205          L     R14,PGM$F                LOAD RETURN ADDR
 00011C 07FE                         206          BR    R14                      RETURN TO CALLER

                                     208          DROP  R12,R13
1                                                                                                               PAGE    8
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 19.18 12/17/20
0                                    210 ***********************************************************************
                                     211 *
                                     212 *        W O R K I N G   S T O R A G E
                                     213 *
                                     214 ***********************************************************************

                                     216          COPY  EREGS
                                     217 ***********************************************************************
                                     218 *
                                     219 *        EQUATES
                                     220 *
                                     221 ***********************************************************************

                                     223 *                                       *******************************
                                     224 *                                       *  REGISTERS
                                     225 *                                       *******************************

                             00000   227 R0       EQU   0
                             00001   228 R1       EQU   1
                             00002   229 R2       EQU   2
                             00003   230 R3       EQU   3
                             00004   231 R4       EQU   4
                             00005   232 R5       EQU   5
                             00006   233 R6       EQU   6
                             00007   234 R7       EQU   7
                             00008   235 R8       EQU   8
                             00009   236 R9       EQU   9
                             0000A   237 R10      EQU   10
                             0000B   238 R11      EQU   11
                             0000C   239 R12      EQU   12
                             0000D   240 R13      EQU   13
                             0000E   241 R14      EQU   14
                             0000F   242 R15      EQU   15

                             00004   244 ERLOGOPN EQU   4                        OPEN FAILED

 00011E D3D6C740D4C5E2E2             246 CCLOGMSG DC    C'LOG MESSAGE'

                                     248 DCBLOG   DCB   DDNAME=LOG,DSORG=PS,RECFM=FB,                           X
                                                        LRECL=80,MACRF=(PM),SYNAD=@LOGSYN
0
                                     250+*                       DATA CONTROL BLOCK                              22770020
                                     251+*                                                                       22860020
 000129 000000
 00012C                              252+DCBLOG   DC    0F'0'                   ORIGIN ON WORD BOUNDARY          22914020

                                     254+*                       DIRECT ACCESS DEVICE INTERFACE                  27360020

 00012C 0000000000000000             256+         DC    BL16'0'                  FDAD,DVTBL                      27540020
 00013C 00000000                     257+         DC    A(0)                     KEYLE,DEVT,TRBAL                27720020

                                     259+*                       COMMON ACCESS METHOD INTERFACE                  48690020

 000140 00                           261+         DC    AL1(0)                  BUFNO                            49050020
1                                                                                                               PAGE    9
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 19.18 12/17/20
0000141 000001                       262+         DC    AL3(1)                  BUFCB                            54720020
 000144 0000                         263+         DC    AL2(0)             BUFL                                  55170020
 000146 4000                         264+         DC    BL2'0100000000000000'                                   *55800020
                                        +                                                 DSORG                  55890020
 000148 00000001                     265+         DC    A(1)                     IOBAD                           56340020

                                     267+*                       FOUNDATION EXTENSION                            56610020

 00014C 00                           269+         DC    BL1'00000000'                  BFTEK,BFLN,HIARCHY        59850020
 00014D 000001                       270+         DC    AL3(1)                  EODAD                            65970020
 000150 90                           271+         DC    BL1'10010000'                                           *66150020
                                        +                                       RECFM                            66240020
 000151 000000                       272+         DC    AL3(0)                  EXLST                            66330020

                                     274+*                       FOUNDATION BLOCK                                66690020

 000154 D3D6C74040404040             276+         DC    CL8'LOG'                DDNAME                           66870020
 00015C 02                           277+         DC    BL1'00000010'           OFLGS                            68220020
 00015D 00                           278+         DC    BL1'00000000'                          IFLG              68310020
 00015E 0050                         279+         DC    BL2'0000000001010000'                                   *68400020
                                        +                                                                       *68490020
                                        +                                       MACR                             68580020

                                     281+*                       BSAM-BPAM-QSAM INTERFACE                        74430020

 000160 00                           283+         DC    BL1'00000000'                                           *74610020
                                        +                                                                   RER1 74700020
 000161 000001                       284+         DC    AL3(1)                  CHECK, GERR, PERR                74790020
 000164 000000DA                     285+         DC    A(@LOGSYN)              SYNAD                            74880020
 000168 0000                         286+         DC    H'0'                    CIND1, CIND2                     74970020
 00016A 0000                         287+         DC    AL2(0)                  BLKSIZE                          75240020
 00016C 00000000                     288+         DC    F'0'                    WCPO, WCPL, OFFSR, OFFSW         75870020
 000170 00000001                     289+         DC    A(1)                    IOBA                             75960020
 000174 00                           290+         DC    AL1(0)                  NCP                              76050020
 000175 000001                       291+         DC    AL3(1)                  EOBR, EOBAD                      76140020

                                     293+*                            QSAM INTERFACE                             81450020

 000178 00000001                     295+         DC    A(1)                     RECAD                           81630020
 00017C 0000                         296+         DC    H'0'                    QSWS                             81810020
 00017E 0050                         297+         DC    AL2(80)        LRECL                                     80730020
 000180 00                           298+         DC    BL1'00000000'           EROPT                            82530020
 000181 000001                       299+         DC    AL3(1)                  CNTRL                            82620020
 000184 00000000                     300+         DC    F'0'                    PRECL                            82710020
 000188 00000001                     301+         DC    A(1)                    EOB                              82800020
1                                                                                                               PAGE   10
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 19.18 12/17/20
0000190                              303          LTORG
1                                                                                                               PAGE   11
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 19.18 12/17/20
0                                    305 ***********************************************************************
                                     306 *
                                     307 *        $PGM       PROGRAM DATA SECTION
                                     308 *
                                     309 ***********************************************************************

 000000                              311 $PGM     DSECT

 000000                              313          DS    18F                      REGISTER SAVE AREA

 000048                              315 PGM$I    DS    F                        @INIT RETURN ADDR
 00004C                              316 PGM$M    DS    F                        @MAIN RETURN ADDR
 000050                              317 PGM$F    DS    F                        @FINAL RETURN ADDR

 000054                              319 WCLOGREC DS    CL80                     OUTPUT RECORD

                             000A4   321 E$PGML   EQU   *-$PGM                   SIZE OF SAVE AREA

1                                                                                                               PAGE   12
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 19.18 12/17/20
0000000                              324          END   @PGM                     END OF PROGRAM
1                                                  RELOCATION DICTIONARY                                        PAGE   13
-POS.ID   REL.ID   FLAGS   ADDRESS                                                                ASM 0201 19.18 12/17/20
0 0001     0001      08     000061
  0001     0001      08     0000F5
  0001     0001      0C     000164

1                                                  CROSS-REFERENCE                                              PAGE   14
-SYMBOL    LEN   VALUE   DEFN    REFERENCES                                                       ASM 0201 19.18 12/17/20
0$PGM     00001 00000000 00311  00030 00091 00133 00178 00321
 @FINAL   00002 000000E4 00175  00060
 @FINL10  00002 00000118 00203  00183
 @INIT    00002 00000058 00088  00051
 @INIT10  00002 00000090 00117  00104
 @LOGSYN  00002 000000DA 00161  00285
 @MAIN    00002 00000096 00130  00056
 @MAIN10  00002 000000D4 00156  00165
 @PGM     00001 00000000 00027  00029 00090 00132 00177 00324
 @PGM10   00002 00000034 00058  00054
 CCLOGMSG 00011 0000011E 00246  00139 00139
 DCBLOG   00004 0000012C 00252  00099 00103 00142 00182 00189
 E$PGML   00001 000000A4 00321  00035 00042 00064
 ERLOGOPN 00001 00000004 00244  00102
 IHB0004A 00002 0000008C 00112  00108
 IHB0007A 00002 000000D2 00153  00149
 IHB0009A 00002 00000114 00198  00194
 PGM$F    00004 00000050 00317  00180 00205
 PGM$I    00004 00000048 00315  00093 00119
 PGM$M    00004 0000004C 00316  00135 00158
 R0       00001 00000000 00227  00035 00064 00070 00076
 R1       00001 00000001 00228  00041 00047 00048 00049 00065 00066 00071 00163
 R10      00001 0000000A 00237
 R11      00001 0000000B 00238
 R12      00001 0000000C 00239  00029 00032 00033 00076 00080 00090 00122 00132 00167 00177 00208
 R13      00001 0000000D 00240  00030 00032 00047 00048 00049 00065 00066 00076 00077 00080 00091 00122 00133 00167 00178
                                00208
 R14      00001 0000000E 00241  00032 00041 00045 00051 00056 00060 00077 00078 00093 00119 00120 00135 00158 00159 00180
                                00205 00206
 R15      00001 0000000F 00242  00033 00042 00053 00053 00062 00074 00102 00115 00115 00163 00164 00201 00201
 R2       00001 00000002 00229  00043 00043 00044 00045 00062 00074
 R3       00001 00000003 00230  00044
 R4       00001 00000004 00231
 R5       00001 00000005 00232
 R6       00001 00000006 00233
 R7       00001 00000007 00234
 R8       00001 00000008 00235
 R9       00001 00000009 00236
 WCLOGREC 00080 00000054 00319  00137 00138 00138 00138 00139 00143
1                                                  ASSEMBLER DIAGNOSTICS AND STATISTICS                         PAGE   15
-                                                                                                 ASM 0201 19.18 12/17/20
0NO STATEMENTS FLAGGED IN THIS ASSEMBLY
 HIGHEST SEVERITY WAS    0
 OPTIONS FOR THIS ASSEMBLY
   ALIGN, ALOGIC, BUFSIZE(STD), DECK, ESD, FLAG(0), LINECOUNT(55), LIST, NOMCALL, YFLAG, WORKSIZE(2097152)
   NOMLOGIC, NONUMBER, OBJECT, RENT, RLD, NOSTMT, NOLIBMAC, NOTERMINAL, NOTEST, XREF
   SYSPARM()
 WORK FILE BUFFER SIZE/NUMBER =19066/ 1
 TOTAL RECORDS READ FROM SYSTEM INPUT        205
 TOTAL RECORDS READ FROM SYSTEM LIBRARY     4342
 TOTAL RECORDS PUNCHED                        12
 TOTAL RECORDS PRINTED                       408
```

### Linker Messages

DWALL01.DEV.SYSPRINT(IEWL) contains linkage editor output messages. The linker finds one CSECT, @PGM. The module is reentrant and reusable.
```
 F64-LEVEL LINKAGE EDITOR OPTIONS SPECIFIED XREF,LET,LIST,NCAL,RENT                                                  
          DEFAULT OPTION(S) USED -  SIZE=(229376,55296)                                                              
                                                                                                                     
                                                CROSS REFERENCE TABLE                                                
                                                                                                                     
  CONTROL SECTION                       ENTRY                                                                        
    NAME    ORIGIN  LENGTH                NAME   LOCATION     NAME   LOCATION     NAME   LOCATION     NAME   LOCATION
  @PGM          00     190                                                                                           
                                                                                                                     
                                                                                                                     
  LOCATION  REFERS TO SYMBOL  IN CONTROL SECTION             LOCATION  REFERS TO SYMBOL  IN CONTROL SECTION          
                                                                                                                     
 ENTRY ADDRESS       00                                                                                              
 TOTAL LENGTH       190                                                                                              
****ASM004    NOW REPLACED IN DATA SET                                                                               
AUTHORIZATION CODE IS         0.                                                                                     
**MODULE HAS BEEN MARKED REENTERABLE, AND REUSABLE.                                                                  
```

### Operator Console Output

Program progress notifcations are written to the operator console using the WTO macro.
```
19.18.29 JOB  154  $HASP100 ASM004   ON INTRDR      DWALL01
19.18.29 JOB  154  $HASP373 ASM004   STARTED - INIT  1 - CLASS A - SYS HMVS
19.18.29 JOB  154  IEF403I ASM004 - STARTED - TIME=19.18.29
19.18.32 JOB  154  IEFACTRT ASM     /IFOX00  /00:00:02.01/00:00:02.79/00000/ASM004
19.18.32 JOB  154  IEFACTRT LINK    /IEWL    /00:00:00.17/00:00:00.37/00000/ASM004
19.18.33 JOB  154  +LOG FILE OPENED
19.18.33 JOB  154  +LOG RECORD WRITTEN
19.18.33 JOB  154  +LOG FILE CLOSED
19.18.33 JOB  154  IEFACTRT RUN     /ASM004  /00:00:00.08/00:00:00.37/00000/ASM004
19.18.33 JOB  154  IEF404I ASM004 - ENDED - TIME=19.18.33
19.18.33 JOB  154  $HASP395 ASM004   ENDED
19.18.33           $HASP309    INIT  1 INACTIVE ******** C=A
19.18.33 JOB  154  $HASP150 ASM004   ON PUNCH1          12 CARDS
19.18.33 JOB  154  $HASP150 ASM004   ON PRINTER1       181 LINES
19.18.33           $HASP160 PUNCH1   INACTIVE - CLASS=B
19.18.33           $HASP160 PRINTER1 INACTIVE - CLASS=A
19.18.33 JOB  154  $HASP250 ASM004   IS PURGED
```

### Output Log File (GDG Generation)

```
 View DWALL01.GDG.LOG.G0021V00--------------------------------------------------
 Cmd =>                                                                         
----+----1----+----2----+----3----+----4----+----5----+----6----+----7----+----8
LOG MESSAGE                                                                     
```

<hr>

Return to [Sample Programs](Samples.md)  
Return to [README](../README.md)
