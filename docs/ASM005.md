## Sample Program ASM005

### Source

DWALL01.DEV.SORLIB(ASM005) writes a record to a new GDG generation.

In the previous sample program, ASM004, we added some QSAM I/O operations using the OPEN, WRITE and CLOSE macros. The macros were invoked inline in our program's initialize, mainline and finalize routines. A logical evolution from that point might have been to isolate I/O into reusable routines within our program. But there are at least three challenges with that approach which are best dealt with now before we add any more complexity. First, there was a tight-coupling between the DCB name and the macro operation in ASM004. If many more data sets need to be accessed within the same program, mutiple macro invocations would be required if the same syntax was used. Since we anticipate potentially using multiple data sets in our programs, a way to refer to a DCB by reference instead of by name would be preferable. Secondly, coding specific I/O macros in the main program CSECT might require a lot of rework if the access method were to change. Instead, if the logical operations such as "open", "close", "read", "write", etc. were more abstracted and the implementation of these was moved to a separate and reusable source, such as a copy book or even a separate module, then changes to the access method could occur while the interface (API) was preserved allowing our main program (the "driver") to remain intact. Thirdly, as we continue to add new features to our program, the size of the driver would soon become large enough to require several base registers to span the entire CSECT. This also can be addressed by "modularizing" the program. Yet, in some circumstances, a subprogram "called" or "linked" by the main program might not be the optimal solution. We would like to have the option of either including such "facilities" in our program as needed or be able to call or link to these methods in another module, while maintaining a single code source for the methods themselves.

Here, we'll take a first step in this direction. ASM005 provides abstraction into the QSAM I/O functions by defining a set of "methods" that share a common set of working storage "members". Access to the "methods" in this program is by a simple branch-and-link (BAL) to a local routine. The routines, though, have been isolated into a separate CSECT defined in a copy book member that is included by the driver. In a later sample, we'll include this same copy book member into a submodule driver to build a reusable module that can be linked or called by our program. This gives us the option of either compiling all facilities into one module or into separate modules.

I'm going to use "object orientation" terminology to describe our QSAM ("file") I/O facility, and other facilities that will be built similarly. The copy book member and CSECT @FIL, which we will copy from our DWALL01.DEV.MACLIB PDS, can be thought of as the implementation of a "class". Allocated storage mapped by the DSECT $FIL can be thought of as an instance of the @FIL class, though it is really just storage for its members. To perform QSAM I/O on a data set, our driver will "create" an instance of the $FIL DSECT by calling a method in the @FIL CSECT that will allocate storage and return its address. This address will then be used as a "pointer to our class instance", similar to a "this" pointer in C++, when invoking other "methods" in @FIL such as "open", "read", "write" and "close". We'll think of this initial allocation of the $FIL instance as our class "constructor". And we'll have a corresponding "destructor" that will release the allocated storage.

In our "driver" (@PGM) we'll define the address of the @FIL CSECT (the "class methods") with an address constant (ADCON) like this, "PGM@FIL DC A(@FIL)". We'll also store the allocated storage address mapped by the $FIL DSECT in our working storage DSECT in "PGM$FIL DS F". This aproach will let us "construct" multiple instances of the $FIL DSECT, that is, multiple instances of our class, while using the same copy of the class methods in the @FIL CSECT.

Each instance of the $FIL maintains its own state in its allocated storage. When dealing with Data Control Blocks (DCBs), however, these are declared first in a CSECT to provide "template" definitions of input and output data sets which are specific to the driver's requirements. When a $FIL instance is constructed (allocated), it will then need to be "initialized" with the DCB template it will use for its I/O operations. This will allow our driver CSECT to remain unaltered through its life-cycle. Whereas a traditional program having a DCB in a CSECT subjects that DCB to changes during its operation. In a later sample, the "allocate" and "initialize" methods could be combined. But, having them as separate methods allows us to re-initialize an $FIL instance for a different DCB without having to deallocate and reallocate storage because all instances of $FIL are of equal size.

I've left ASM005 with code that specifically loads the @FIL and $FIL addresses before calls are made to each of the @FIL methods. This can be simplified by avoiding loading base registers where we can be sure those values have not changed. Also, in a later sample program, we'll look at defining macros for these calls into our class methods to simplify and standardize how the APIs are invoked.

Our main driver function has not changed since ASM004. We're still calling Init, Main and Final. In Init, instead of invoking OPEN directly, we call @FILA and @FILI to allocate an instance of our $FIL class and initialize it with the DCB template for our log file. We'll also call @FILO to open the data set. In Main, we'll call @FILW to write a message. In Final, we close the data set and release $FIL storage. Each of these methods require CSECT and DSECT addresses which are passed in R10 and R11. When we extend this design to additional facilities, we'll reuse these same registers, R10 for the CSECT of the facility and R11 for the instance of the DSECT. This will allow us to invoke multiple instances of multiple facilities all while using only two registers for addressing this facilities. 
```
***********************************************************************
*
*        PROGRAM    ASM005
*
*        THIS PROGRAM WRITES A RECORD TO A DATA SET. REUSABLE I/O
*        ROUTINES ARE PLACED IN A COPY BOOK MEMBER, @FIL, IN THE CSECT
*        @FIL. THE DSECT $FIL DEFINES THE DATA "MEMBERS" OF THE @FIL
*        "CLASS". $FIL IS ALLOCATED BY THE METHOD @FILA. WHEN CALLING
*        @FIL METHODS, R10 ADDRESSES THE CSECT AND R11 THE ALLOCATED
*        STORAGE FOR THE $FIL INSTANCE.
*
*        ENTRY      R1      PROGRAM ARGUMENTS ADDRESS
*                   R13     CALLER SAVE AREA ADDRESS
*                   R14     RETURN ADDRESS
*                   R15     CONTROL SECTION ADDRESS
*
*        EXIT       R14     RETURN ADDRESS
*                   R15     RESULT CODE
*
***********************************************************************
         EJECT
***********************************************************************
*
*        @PGM       PROGRAM CONTROL SECTION
*
***********************************************************************
         SPACE
@PGM     CSECT                          PROGRAM ENTRY
         SPACE
         USING @PGM,R12
         USING $PGM,R13
         SPACE
         STM   R14,R12,12(R13)          SAVE CALLER REGS
         LR    R12,R15                  @PGM BASE
         SPACE
         LA    R0,E$PGML                LENGTH OF STORAGE
         SPACE
         GETMAIN R,LV=(0)               GET STORAGE ADDR IN REG 1
         SPACE
         LR    R14,R1                   $PGM ADDR
         LA    R15,E$PGML               $PGM SIZE
         XR    R2,R2                    SOURCE ADDR (NO COPY)
         LR    R3,R2                    SOURCE LENGTH (PAD)
         MVCL  R14,R2                   CLEAR STORAGE TO ZEROS
         SPACE
         ST    R13,4(,R1)               SAVE PARENT SAVE AREA ADDR
         ST    R1,8(,R13)               SAVE CURRENT SAVE AREA ADDR
         LR    R13,R1                   LOAD SAVE AREA ADDR
         SPACE
         BAL   R14,@INIT                INITIALIZE PROGRAM
         SPACE
         LTR   R15,R15                  IF INITIALIZATION SUCCEEDED
         BNZ   @PGM10
         SPACE
         BAL   R14,@MAIN                  DO MAINLINE LOGIC
         SPACE
@PGM10   DS    0H                       END IF
         SPACE
         BAL   R14,@FINAL               FINALIZE PROGRAM
         SPACE
         LR    R2,R15                   HOLD RESULT CODE
         SPACE
         LA    R0,E$PGML                LENGTH OF STORAGE
         LR    R1,R13                   SAVE AREA ADDR
         L     R13,4(,R1)               RESTORE PARENT SAVEAREA ADDR
         SPACE
         FREEMAIN R,LV=(R0),A=(R1)      RELEASE STORAGE
         SPACE
         LR    R15,R2                   RESULT CODE
         SPACE
         LM    R0,R12,20(R13)           LOAD CALLER REGS
         L     R14,12(,R13)             LOAD RETURN ADDR
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R12,R13
         EJECT
***********************************************************************
*
*        @INIT      INITIALIZE PROGRAM
*
***********************************************************************
         SPACE
@INIT    DS    0H
         SPACE
         USING @PGM,R12
         USING $PGM,R13
         USING @FIL,R10
         SPACE
         ST    R14,PGM$I                SAVE RETURN ADDR (,13)
         SPACE
         L     R10,PGM@FIL              @FIL ADDR (,12)
         BAL   R14,@FILA                ALLOCATE $FIL (,10)
         SPACE
         ST    R1,PGM$FIL               SAVE $FIL ADDR (,13)
         SPACE
         WTO   '@FILA OK'               NOTIFY CONSOLE
         SPACE
         LA    R0,FILETYPO              OUTPUT FILE
         LA    R1,DCBLOG                LOG DCB ADDR (,12)
         L     R10,PGM@FIL              @FIL ADDR (,12)
         L     R11,PGM$FIL              $FIL ADDR (,13)
         BAL   R14,@FILI                INITIALIZE $FIL (,10)
         SPACE
         WTO   '@FILI OK'               NOTIFY CONSOLE
         SPACE
         L     R10,PGM@FIL              @FIL ADDR (,12)
         L     R11,PGM$FIL              $FIL ADDR (,13)
         BAL   R14,@FILO                OPEN FILE (,10)
         SPACE
         LTR   R15,R15                  IF FILE OPEN
         BNZ   @INIT10
         SPACE
         WTO   '@FILO OK'                 NOTIFY CONSOLE
         SPACE
         XR    R15,R15                    ZERO RESULT CODE
         SPACE
@INIT10  DS    0H                       END IF
         SPACE
         L     R14,PGM$I                LOAD RETURN ADDR (,13)
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R12,R13,R10
         EJECT
***********************************************************************
*
*        @MAIN      MAIN LOGIC LOOP
*
***********************************************************************
         SPACE
@MAIN    DS    0H
         SPACE
         USING @PGM,R12
         USING $PGM,R13
         USING @FIL,R10
         SPACE
         ST    R14,PGM$M                SAVE RETURN ADDR
         SPACE
         MVI   WCLOGREC,C' '                       CLEAR RECORD
         MVC   WCLOGREC+1(L'WCLOGREC-1),WCLOGREC   TO SPACES
         MVC   WCLOGREC(L'CCLOGMSG),CCLOGMSG       STORE MESSAGE
         SPACE
         LA    R0,WCLOGREC              RECORD ADDR (,12)
         L     R10,PGM@FIL              @FIL ADDR (,12)
         L     R11,PGM$FIL              $FIL ADDR (,13)
         BAL   R14,@FILW                WRITE RECORD
         SPACE
         LTR   R15,R15                  IF RECORD WRITTEN
         BNZ   @MAIN10
         SPACE
         WTO   '@FILW OK'                 NOTIFY CONSOLE
         SPACE
@MAIN10  DS    0H                       END IF
         SPACE
         L     R14,PGM$M                LOAD RETURN ADDR
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R12,R13,R10
         EJECT
***********************************************************************
*
*        @FINAL     FINALIZATION ROUTINE
*
***********************************************************************
         SPACE
@FINAL   DS    0H
         SPACE
         USING @PGM,R12
         USING $PGM,R13
         USING @FIL,R10
         SPACE
         ST    R14,PGM$F                SAVE RETURN ADDR
         SPACE
         L     R10,PGM@FIL              @FIL ADDR (,12)
         L     R11,PGM$FIL              $FIL ADDR (,13)
         BAL   R14,@FILC                CLOSE FILE (,10)
         SPACE
         LTR   R15,R15                  IF FILE CLOSED
         BNZ   @FINL10
         SPACE
         WTO   '@FILC OK'                 NOTIFY CONSOLE
         SPACE
@FINL10  DS    0H                       END IF
         SPACE
         L     R10,PGM@FIL              @FIL ADDR (,12)
         L     R11,PGM$FIL              $FIL ADDR (,13)
         BAL   R14,@FILZ                RELEASE FILE (,10)
         SPACE
         WTO   '@FILZ OK'               NOTIFY CONSOLE
         SPACE
         XR    R15,R15                  ZERO RESULT CODE
         SPACE
         L     R14,PGM$F                LOAD RETURN ADDR
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R12,R13,R10
         EJECT
***********************************************************************
*
*        W O R K I N G   S T O R A G E
*
***********************************************************************
         SPACE
         COPY  EREGS
         SPACE
PGM@FIL  DC    A(@FIL)                  @FIL ADDR
         SPACE
CCLOGMSG DC    C'LOG MESSAGE'
         SPACE
DCBLOG   DCB   DDNAME=LOG,DSORG=PS,RECFM=FB,                           X
               LRECL=80,MACRF=(PM),SYNAD=@FILWSYN
         EJECT
         LTORG
         EJECT
***********************************************************************
*
*        $PGM       PROGRAM DATA SECTION
*
***********************************************************************
         SPACE
$PGM     DSECT
         SPACE
         DS    18F                      REGISTER SAVE AREA
         SPACE
PGM$I    DS    F                        @INIT RETURN ADDR
PGM$M    DS    F                        @MAIN RETURN ADDR
PGM$F    DS    F                        @FINAL RETURN ADDR
         SPACE
PGM$FIL  DS    F                        $FIL ADDR
         SPACE
WCLOGREC DS    CL80                     OUTPUT RECORD
         SPACE
E$PGML   EQU   *-$PGM                   SIZE OF SAVE AREA
         SPACE
         EJECT
***********************************************************************
*
*        INCLUDED OBJECTS
*
***********************************************************************
         SPACE
         COPY  @FIL                     FILE CLASS
         END   @PGM                     END OF PROGRAM
```

### Copy Book @FIL

DWALL01.DEV.MACLIB(@FIL) implements the methods of the @FIL class, each instance of which is a reference the $FIL DSECT in allocated storage.
```
***********************************************************************
*
*        @FIL       FILE FACILITY
*
*        @FILA      ALLOCATE $FIL STORAGE
*        @FILI      INITIALIZE $FIL
*        @FILO      OPEN THE FILE
*        @FILR      READ FROM THE FILE
*        @FILW      WRITE TO THE FILE
*        @FILC      CLOSE THE FILE
*        @FILZ      RELEASE $FIL STORAGE
*
*        COPYRIGHT 2010-2021 DAVID J WALLING. MIT LICENSE.
*
***********************************************************************
         EJECT
***********************************************************************
*
*        @FIL       FILE CONTROL SECTION
*
***********************************************************************
         SPACE
@FIL     CSECT
         EJECT
***********************************************************************
*
*        @FILA      ALLOCATE STORAGE FOR INSTANCE
*
*        IN         R10     @FIL CSECT ADDR
*                   R12     CALLER CSECT ADDR
*                   R13     CALLER DATA ADDR (SAVE AREA)
*                   R14     RETURN ADDRESS
*
*        OUT        R1      $FIL ADDR (OR ABEND)
*                   R15     GETMAIN RESULT (OR ABEND)
*
***********************************************************************
         SPACE
@FILA    DS    0H
         SPACE
         USING @FIL,R10
         SPACE
         LR    R2,R14                   HOLD RETURN ADDR
         LA    R0,E$FILL                $FIL SIZE
         SPACE
         GETMAIN R,LV=(0)               ALLOCATE STORAGE (,10)
         SPACE
         LR    R14,R2                   LOAD RETURN ADDR
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R10
         EJECT
***********************************************************************
*
*        @FILI      INITIALIZE $FIL CONTEXT
*
*        IN         R0      DCB OPTION BYTE (BITS 24-31)
*                   R1      DCB TEMPLATE ADDR
*                   R10     @FIL CSECT ADDR
*                   R11     $FIL DSECT ADDR
*                   R12     CALLER CSECT ADDR
*                   R13     CALLER DATA ADDR (SAVE AREA)
*                   R14     RETURN ADDRESS
*
***********************************************************************
         SPACE
@FILI    DS    0H
         SPACE
         USING $FIL,R11
         SPACE
         ST    R14,FIL$I                SAVE RETURN ADDR (,11)
         SPACE
         STC   R0,FILXOPT               SAVE DCB OPTION BYTE (,11)
         MVC   FILXDCB,0(R1)            COPY DCB TEMPLATE (,11)
         SPACE
         L     R14,FIL$I                LOAD RETURN ADDR (,11)
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R11
         EJECT
***********************************************************************
*
*        @FILO      OPEN THE FILE
*
*        IN         R10     @FIL CSECT ADDR
*                   R11     $FIL DSECT ADDR
*                   R12     CALLER CSECT ADDR
*                   R13     CALLER DATA ADDR (SAVE AREA)
*                   R14     RETURN ADDRESS
*
*        OUT        R15     OPEN RESULT CODE
*
***********************************************************************
         SPACE
@FILO    DS    0H
         SPACE
         USING $FIL,R11
         SPACE
         ST    R14,FIL$O                SAVE RETURN ADDR (,11)
         SPACE
         LA    R1,FILFARG               PARMLIST ADDR (,11)
         LA    R0,FILXDCB               DCB ADDR (,11)
         ST    R0,0(,R1)                PUT DCB ADDR IN PARMLIST
         MVC   0(1,R1),FILXOPT          SET HI BYTE TO OPTION
         SPACE
         SVC   19                       ISSUE OPEN
         SPACE
         L     R14,FIL$O                LOAD RETURN ADDR (,11)
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R11
         EJECT
***********************************************************************
*
*        @FILR      READ THE FILE
*
*        IN         R0      TARGET ADDR FOR READ
*                   R10     @FIL CSECT ADDR
*                   R11     $FIL DSECT ADDR
*                   R12     CALLER CSECT ADDR
*                   R13     CALLER DATA ADDR (SAVE AREA)
*                   R14     RETURN ADDRESS
*
*        OUT        R15     GET RESULT CODE
*
*        GET ROUTINE MAY USE SAVE AREA AT R13
*
***********************************************************************
         SPACE
@FILR    DS    0H
         SPACE
         USING $FIL,R11
         SPACE
         ST    R14,FIL$R                SAVE RETURN ADDR (,11)
         SPACE
         LA    R1,FILXDCB               DCB ADDR (,11)
         L     R15,48(,R1)              DCB GET ROUTINE ADDR
         SPACE
         BALR  R14,R15                  CALL GET ROUTINE
         SPACE
         L     R14,FIL$R                LOAD RETURN ADDR (,11)
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R11
         EJECT
***********************************************************************
*
*        @FILW      WRITE TO THE FILE
*
*        IN         R0      SOURCE ADDR FOR WRITE
*                   R10     @FIL CSECT ADDR
*                   R11     $FIL DSECT ADDR
*                   R12     CALLER CSECT ADDR
*                   R13     CALLER DATA ADDR (SAVE AREA)
*                   R14     RETURN ADDRESS
*
*        OUT        R15     PUT RESULT CODE
*
*        PUT ROUTINE MAY USE SAVE AREA AT 13
*
***********************************************************************
         SPACE
@FILW    DS    0H
         SPACE
         USING @FIL,R10
         USING $FIL,R11
         SPACE
         ST    R14,FIL$W                SAVE RETURN ADDR (,11)
         SPACE
         LA    R1,FILXDCB               DCB ADDR (,11)
         L     R15,48(,R1)              DCB PUT ROUTINE ADDR
         SPACE
         BALR  R14,R15                  CALL PUT ROUTINE
         SPACE
         XR    R15,R15                  ZERO RESULT IF NO SYNAD
         SPACE
@FILW80  DS    0H                       SYNAD RETURNS HERE
         SPACE
         L     R14,FIL$W                LOAD RETURN ADDR (,11)
         BR    R14                      RETURN TO CALLER
         SPACE
@FILWSYN DS    0H                       SYNAD ENTRY
         SPACE
         LR    R15,R1                   ERROR BITS 0-7
         SRL   R15,24                   ERROR BITS 24-31
         SPACE
         B     @FILW80                  CONTINUE
         SPACE
         DROP  R10,R11
         EJECT
***********************************************************************
*
*        @FILC      CLOSE THE FILE
*
*        IN         R10     @FIL CSECT ADDR
*                   R11     $FIL DSECT ADDR
*                   R12     CALLER CSECT ADDR
*                   R13     CALLER DATA ADDR (SAVE AREA)
*                   R14     RETURN ADDRESS
*
*        OUT        R15     SVC 20 RESULT CODE
*
***********************************************************************
         SPACE
@FILC    DS    0H
         SPACE
         USING $FIL,R11
         SPACE
         ST    R14,FIL$C                SAVE RETURN ADDR (,11)
         SPACE
         LA    R1,FILFARG               PARMLIST ADDR (,11)
         LA    R0,FILXDCB               DCB ADDR (,11)
         ST    R0,0(,R1)                PUT DCB ADDR IN PARMLIST
         MVI   0(R1),X'80'              MARK AS LAST PARM
         SPACE
         SVC   20                       CLOSE FILE
         SPACE
         L     R14,FIL$C                LOAD RETURN ADDR (,11)
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R11
         EJECT
***********************************************************************
*
*        @FILZ      FINALIZE $FIL OBJECT
*
*        IN         R10     @FIL CSECT ADDR
*                   R11     $FIL DSECT ADDR
*                   R12     CALLER CSECT ADDR
*                   R13     CALLER DATA ADDR (SAVE AREA)
*                   R14     RETURN ADDRESS
*
*        OUT        R15     FREEMAIN RESULT CODE (OR ABEND)
*
***********************************************************************
         SPACE
@FILZ    DS    0H
         SPACE
         USING @FIL,R10
         SPACE
         LR    R2,R14                   HOLD RETURN ADDR
         LR    R1,R11                   $FIL ADDR
         LA    R0,E$FILL                $FIL LENGTH
         SPACE
         FREEMAIN R,LV=(0),A=(1)        RELEASE STORAGE (,10)
         SPACE
         LR    R14,R2                   LOAD RETURN ADDR
         BR    R14                      RETURN TO CALLER
         SPACE
         DROP  R10
         EJECT
***********************************************************************
*
*        W O R K I N G   S T O R A G E
*
***********************************************************************
         SPACE
FILETYPI EQU   X'80'                    INPUT FILE OPTION BYTE
FILETYPO EQU   X'8F'                    OUTPUT FILE OPTION BYTE
         SPACE
***********************************************************************
*
*        $FIL       FILE DATA SECTION
*
***********************************************************************
         SPACE
$FIL     DSECT
         SPACE
FIL$I    DS    F                        @FILI RETURN ADDR
FIL$O    DS    F                        @FILO RETURN ADDR
FIL$R    DS    F                        @FILR RETURN ADDR
FIL$W    DS    F                        @FILW RETURN ADDR
FIL$C    DS    F                        @FILC RETURN ADDR
         SPACE
FILFARG  DS    F                        SVC 19,20 INPUT
         SPACE
         DS    0F                       FULLWORD ALIGN DCB
FILXDCB  DS    XL96                     DCB COPY
FILXOPT  DS    X                        OPTION BYTE
         SPACE
E$FILL   EQU   *-$FIL                   DATA SECTION LENGTH
         EJECT
```

### Job Control Language

DWALL01.DEV.JCLLIB(ASM005) invokes SYS2.PROCLIB(ASMLKED) which assembles, links and runs the program.

JS010 invokes the procedure ASMLKED, passing "ASM005" as the M parameter value.
```
//ASM005   JOB (001),'DWALL01',CLASS=A,MSGCLASS=A,   
//             MSGLEVEL=(1,1),NOTIFY=DWALL01         
//*                                                  
//*            ASSEMBLE, LINK AND RUN                
//*                                                  
//JS010   EXEC ASMLKED,M=ASM005                      
//                                                   
```

### Assembly Listing

No literals were used in the program, so the literal pool (LTORG) is empty

```
1                                               EXTERNAL SYMBOL DICTIONARY                                      PAGE    1
-SYMBOL   TYPE  ID   ADDR  LENGTH LDID                                                            ASM 0201 20.15 12/17/20
0@PGM      SD  0001 000000 0001D8
 @FIL      SD  0002 0001D8 0000A4
1                                                                                                               PAGE    2
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                      1 ***********************************************************************
                                       2 *
                                       3 *        PROGRAM    ASM005
                                       4 *
                                       5 *        THIS PROGRAM WRITES A RECORD TO A DATA SET. REUSABLE I/O
                                       6 *        ROUTINES ARE PLACED IN A COPY BOOK MEMBER, @FIL, IN THE CSECT
                                       7 *        @FIL. THE DSECT $FIL DEFINES THE DATA "MEMBERS" OF THE @FIL
                                       8 *        "CLASS". $FIL IS ALLOCATED BY THE METHOD @FILA. WHEN CALLING
                                       9 *        @FIL METHODS, R10 ADDRESSES THE CSECT AND R11 THE ALLOCATED
                                      10 *        STORAGE FOR THE $FIL INSTANCE.
                                      11 *
                                      12 *        ENTRY      R1      PROGRAM ARGUMENTS ADDRESS
                                      13 *                   R13     CALLER SAVE AREA ADDRESS
                                      14 *                   R14     RETURN ADDRESS
                                      15 *                   R15     CONTROL SECTION ADDRESS
                                      16 *
                                      17 *        EXIT       R14     RETURN ADDRESS
                                      18 *                   R15     RESULT CODE
                                      19 *
                                      20 ***********************************************************************
1                                                                                                               PAGE    3
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                     22 ***********************************************************************
                                      23 *
                                      24 *        @PGM       PROGRAM CONTROL SECTION
                                      25 *
                                      26 ***********************************************************************

 000000                               28 @PGM     CSECT                          PROGRAM ENTRY

                             00000    30          USING @PGM,R12
                             00000    31          USING $PGM,R13

 000000 90EC D00C      0000C          33          STM   R14,R12,12(R13)          SAVE CALLER REGS
 000004 18CF                          34          LR    R12,R15                  @PGM BASE

 000006 4100 00A8      000A8          36          LA    R0,E$PGML                LENGTH OF STORAGE

                                      38          GETMAIN R,LV=(0)               GET STORAGE ADDR IN REG 1
 00000A 4510 C00E      0000E          39+         BAL   1,*+4                             INDICATE GETMAIN
 00000E 0A0A                          40+         SVC   10                                ISSUE GETMAIN SVC

 000010 18E1                          42          LR    R14,R1                   $PGM ADDR
 000012 41F0 00A8      000A8          43          LA    R15,E$PGML               $PGM SIZE
 000016 1722                          44          XR    R2,R2                    SOURCE ADDR (NO COPY)
 000018 1832                          45          LR    R3,R2                    SOURCE LENGTH (PAD)
 00001A 0EE2                          46          MVCL  R14,R2                   CLEAR STORAGE TO ZEROS

 00001C 50D0 1004      00004          48          ST    R13,4(,R1)               SAVE PARENT SAVE AREA ADDR
 000020 5010 D008      00008          49          ST    R1,8(,R13)               SAVE CURRENT SAVE AREA ADDR
 000024 18D1                          50          LR    R13,R1                   LOAD SAVE AREA ADDR

 000026 45E0 C058      00058          52          BAL   R14,@INIT                INITIALIZE PROGRAM

 00002A 12FF                          54          LTR   R15,R15                  IF INITIALIZATION SUCCEEDED
 00002C 4770 C034      00034          55          BNZ   @PGM10

 000030 45E0 C0CE      000CE          57          BAL   R14,@MAIN                  DO MAINLINE LOGIC

 000034                               59 @PGM10   DS    0H                       END IF

 000034 45E0 C110      00110          61          BAL   R14,@FINAL               FINALIZE PROGRAM

 000038 182F                          63          LR    R2,R15                   HOLD RESULT CODE

 00003A 4100 00A8      000A8          65          LA    R0,E$PGML                LENGTH OF STORAGE
 00003E 181D                          66          LR    R1,R13                   SAVE AREA ADDR
 000040 58D0 1004      00004          67          L     R13,4(,R1)               RESTORE PARENT SAVEAREA ADDR

                                      69          FREEMAIN R,LV=(R0),A=(R1)      RELEASE STORAGE
                                      70+*      OS/VS2 RELEASE 3 VERSION  -- 10/25/74                            00001603
 000044 1800                          71+         LR    0,R0                              LOAD LENGTH            00192003
 000046 4110 1000      00000          72+         LA    1,0(0,R1)                         LOAD AREA ADDRESS      00164002
 00004A 0A0A                          73+         SVC   10                                ISSUE FREEMAIN SVC     00311202

 00004C 18F2                          75          LR    R15,R2                   RESULT CODE

1                                                                                                               PAGE    4
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
000004E 980C D014      00014          77          LM    R0,R12,20(R13)           LOAD CALLER REGS
 000052 58E0 D00C      0000C          78          L     R14,12(,R13)             LOAD RETURN ADDR
 000056 07FE                          79          BR    R14                      RETURN TO CALLER

                                      81          DROP  R12,R13
1                                                                                                               PAGE    5
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                     83 ***********************************************************************
                                      84 *
                                      85 *        @INIT      INITIALIZE PROGRAM
                                      86 *
                                      87 ***********************************************************************

 000058                               89 @INIT    DS    0H

                             00000    91          USING @PGM,R12
                             00000    92          USING $PGM,R13
                             001D8    93          USING @FIL,R10

 000058 50E0 D048      00048          95          ST    R14,PGM$I                SAVE RETURN ADDR (,13)

 00005C 58A0 C164      00164          97          L     R10,PGM@FIL              @FIL ADDR (,12)
 000060 45E0 A000      001D8          98          BAL   R14,@FILA                ALLOCATE $FIL (,10)

 000064 5010 D054      00054         100          ST    R1,PGM$FIL               SAVE $FIL ADDR (,13)

                                     102          WTO   '@FILA OK'               NOTIFY CONSOLE
 000068                              103+         CNOP  0,4                                                      09800002
 000068 4510 C078      00078         104+         BAL   1,IHB0003A                        BRANCH AROUND MESSAGE  09850002
 00006C 000C                         105+         DC    AL2(12)             TEXT LENGTH                          13200002
 00006E 0000                         106+         DC    B'0000000000000000' MCS FLAGS                            13250002
 000070 7CC6C9D3C140D6D2             107+         DC    C'@FILA OK'                                              13350002
 000078                              108+IHB0003A DS    0H                                                       15850002
 000078 0A23                         109+         SVC   35                                                       15950002

 00007A 4100 008F      0008F         111          LA    R0,FILETYPO              OUTPUT FILE
 00007E 4110 C174      00174         112          LA    R1,DCBLOG                LOG DCB ADDR (,12)
 000082 58A0 C164      00164         113          L     R10,PGM@FIL              @FIL ADDR (,12)
 000086 58B0 D054      00054         114          L     R11,PGM$FIL              $FIL ADDR (,13)
 00008A 45E0 A010      001E8         115          BAL   R14,@FILI                INITIALIZE $FIL (,10)

                                     117          WTO   '@FILI OK'               NOTIFY CONSOLE
 00008E 0700                         118+         CNOP  0,4                                                      09800002
 000090 4510 C0A0      000A0         119+         BAL   1,IHB0004A                        BRANCH AROUND MESSAGE  09850002
 000094 000C                         120+         DC    AL2(12)             TEXT LENGTH                          13200002
 000096 0000                         121+         DC    B'0000000000000000' MCS FLAGS                            13250002
 000098 7CC6C9D3C940D6D2             122+         DC    C'@FILI OK'                                              13350002
 0000A0                              123+IHB0004A DS    0H                                                       15850002
 0000A0 0A23                         124+         SVC   35                                                       15950002

 0000A2 58A0 C164      00164         126          L     R10,PGM@FIL              @FIL ADDR (,12)
 0000A6 58B0 D054      00054         127          L     R11,PGM$FIL              $FIL ADDR (,13)
 0000AA 45E0 A024      001FC         128          BAL   R14,@FILO                OPEN FILE (,10)

 0000AE 12FF                         130          LTR   R15,R15                  IF FILE OPEN
 0000B0 4770 C0C8      000C8         131          BNZ   @INIT10

                                     133          WTO   '@FILO OK'                 NOTIFY CONSOLE
 0000B4                              134+         CNOP  0,4                                                      09800002
 0000B4 4510 C0C4      000C4         135+         BAL   1,IHB0005A                        BRANCH AROUND MESSAGE  09850002
 0000B8 000C                         136+         DC    AL2(12)             TEXT LENGTH                          13200002
 0000BA 0000                         137+         DC    B'0000000000000000' MCS FLAGS                            13250002
1                                                                                                               PAGE    6
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
00000BC 7CC6C9D3D640D6D2             138+         DC    C'@FILO OK'                                              13350002
 0000C4                              139+IHB0005A DS    0H                                                       15850002
 0000C4 0A23                         140+         SVC   35                                                       15950002

 0000C6 17FF                         142          XR    R15,R15                    ZERO RESULT CODE

 0000C8                              144 @INIT10  DS    0H                       END IF

 0000C8 58E0 D048      00048         146          L     R14,PGM$I                LOAD RETURN ADDR (,13)
 0000CC 07FE                         147          BR    R14                      RETURN TO CALLER

                                     149          DROP  R12,R13,R10
1                                                                                                               PAGE    7
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                    151 ***********************************************************************
                                     152 *
                                     153 *        @MAIN      MAIN LOGIC LOOP
                                     154 *
                                     155 ***********************************************************************

 0000CE                              157 @MAIN    DS    0H

                             00000   159          USING @PGM,R12
                             00000   160          USING $PGM,R13
                             001D8   161          USING @FIL,R10

 0000CE 50E0 D04C      0004C         163          ST    R14,PGM$M                SAVE RETURN ADDR

 0000D2 9240 D058      00058         165          MVI   WCLOGREC,C' '                       CLEAR RECORD
 0000D6 D24E D059 D058 00059 00058   166          MVC   WCLOGREC+1(L'WCLOGREC-1),WCLOGREC   TO SPACES
 0000DC D20A D058 C168 00058 00168   167          MVC   WCLOGREC(L'CCLOGMSG),CCLOGMSG       STORE MESSAGE

 0000E2 4100 D058      00058         169          LA    R0,WCLOGREC              RECORD ADDR (,12)
 0000E6 58A0 C164      00164         170          L     R10,PGM@FIL              @FIL ADDR (,12)
 0000EA 58B0 D054      00054         171          L     R11,PGM$FIL              $FIL ADDR (,13)
 0000EE 45E0 A056      0022E         172          BAL   R14,@FILW                WRITE RECORD

 0000F2 12FF                         174          LTR   R15,R15                  IF RECORD WRITTEN
 0000F4 4770 C10A      0010A         175          BNZ   @MAIN10

                                     177          WTO   '@FILW OK'                 NOTIFY CONSOLE
 0000F8                              178+         CNOP  0,4                                                      09800002
 0000F8 4510 C108      00108         179+         BAL   1,IHB0006A                        BRANCH AROUND MESSAGE  09850002
 0000FC 000C                         180+         DC    AL2(12)             TEXT LENGTH                          13200002
 0000FE 0000                         181+         DC    B'0000000000000000' MCS FLAGS                            13250002
 000100 7CC6C9D3E640D6D2             182+         DC    C'@FILW OK'                                              13350002
 000108                              183+IHB0006A DS    0H                                                       15850002
 000108 0A23                         184+         SVC   35                                                       15950002

 00010A                              186 @MAIN10  DS    0H                       END IF

 00010A 58E0 D04C      0004C         188          L     R14,PGM$M                LOAD RETURN ADDR
 00010E 07FE                         189          BR    R14                      RETURN TO CALLER

                                     191          DROP  R12,R13,R10
1                                                                                                               PAGE    8
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                    193 ***********************************************************************
                                     194 *
                                     195 *        @FINAL     FINALIZATION ROUTINE
                                     196 *
                                     197 ***********************************************************************

 000110                              199 @FINAL   DS    0H

                             00000   201          USING @PGM,R12
                             00000   202          USING $PGM,R13
                             001D8   203          USING @FIL,R10

 000110 50E0 D050      00050         205          ST    R14,PGM$F                SAVE RETURN ADDR

 000114 58A0 C164      00164         207          L     R10,PGM@FIL              @FIL ADDR (,12)
 000118 58B0 D054      00054         208          L     R11,PGM$FIL              $FIL ADDR (,13)
 00011C 45E0 A076      0024E         209          BAL   R14,@FILC                CLOSE FILE (,10)

 000120 12FF                         211          LTR   R15,R15                  IF FILE CLOSED
 000122 4770 C13A      0013A         212          BNZ   @FINL10

                                     214          WTO   '@FILC OK'                 NOTIFY CONSOLE
 000126 0700                         215+         CNOP  0,4                                                      09800002
 000128 4510 C138      00138         216+         BAL   1,IHB0007A                        BRANCH AROUND MESSAGE  09850002
 00012C 000C                         217+         DC    AL2(12)             TEXT LENGTH                          13200002
 00012E 0000                         218+         DC    B'0000000000000000' MCS FLAGS                            13250002
 000130 7CC6C9D3C340D6D2             219+         DC    C'@FILC OK'                                              13350002
 000138                              220+IHB0007A DS    0H                                                       15850002
 000138 0A23                         221+         SVC   35                                                       15950002

 00013A                              223 @FINL10  DS    0H                       END IF

 00013A 58A0 C164      00164         225          L     R10,PGM@FIL              @FIL ADDR (,12)
 00013E 58B0 D054      00054         226          L     R11,PGM$FIL              $FIL ADDR (,13)
 000142 45E0 A092      0026A         227          BAL   R14,@FILZ                RELEASE FILE (,10)

                                     229          WTO   '@FILZ OK'               NOTIFY CONSOLE
 000146 0700                         230+         CNOP  0,4                                                      09800002
 000148 4510 C158      00158         231+         BAL   1,IHB0008A                        BRANCH AROUND MESSAGE  09850002
 00014C 000C                         232+         DC    AL2(12)             TEXT LENGTH                          13200002
 00014E 0000                         233+         DC    B'0000000000000000' MCS FLAGS                            13250002
 000150 7CC6C9D3E940D6D2             234+         DC    C'@FILZ OK'                                              13350002
 000158                              235+IHB0008A DS    0H                                                       15850002
 000158 0A23                         236+         SVC   35                                                       15950002

 00015A 17FF                         238          XR    R15,R15                  ZERO RESULT CODE

 00015C 58E0 D050      00050         240          L     R14,PGM$F                LOAD RETURN ADDR
 000160 07FE                         241          BR    R14                      RETURN TO CALLER

                                     243          DROP  R12,R13,R10
1                                                                                                               PAGE    9
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                    245 ***********************************************************************
                                     246 *
                                     247 *        W O R K I N G   S T O R A G E
                                     248 *
                                     249 ***********************************************************************

                                     251          COPY  EREGS
                                     252 ***********************************************************************
                                     253 *
                                     254 *        EQUATES
                                     255 *
                                     256 ***********************************************************************

                                     258 *                                       *******************************
                                     259 *                                       *  REGISTERS
                                     260 *                                       *******************************

                             00000   262 R0       EQU   0
                             00001   263 R1       EQU   1
                             00002   264 R2       EQU   2
                             00003   265 R3       EQU   3
                             00004   266 R4       EQU   4
                             00005   267 R5       EQU   5
                             00006   268 R6       EQU   6
                             00007   269 R7       EQU   7
                             00008   270 R8       EQU   8
                             00009   271 R9       EQU   9
                             0000A   272 R10      EQU   10
                             0000B   273 R11      EQU   11
                             0000C   274 R12      EQU   12
                             0000D   275 R13      EQU   13
                             0000E   276 R14      EQU   14
                             0000F   277 R15      EQU   15

 000162 0000
 000164 000001D8                     279 PGM@FIL  DC    A(@FIL)                  @FIL ADDR

 000168 D3D6C740D4C5E2E2             281 CCLOGMSG DC    C'LOG MESSAGE'

                                     283 DCBLOG   DCB   DDNAME=LOG,DSORG=PS,RECFM=FB,                           X
                                                        LRECL=80,MACRF=(PM),SYNAD=@FILWSYN
0
                                     285+*                       DATA CONTROL BLOCK                              22770020
                                     286+*                                                                       22860020
 000173 00
 000174                              287+DCBLOG   DC    0F'0'                   ORIGIN ON WORD BOUNDARY          22914020

                                     289+*                       DIRECT ACCESS DEVICE INTERFACE                  27360020

 000174 0000000000000000             291+         DC    BL16'0'                  FDAD,DVTBL                      27540020
 000184 00000000                     292+         DC    A(0)                     KEYLE,DEVT,TRBAL                27720020

                                     294+*                       COMMON ACCESS METHOD INTERFACE                  48690020

1                                                                                                               PAGE   10
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0000188 00                           296+         DC    AL1(0)                  BUFNO                            49050020
 000189 000001                       297+         DC    AL3(1)                  BUFCB                            54720020
 00018C 0000                         298+         DC    AL2(0)             BUFL                                  55170020
 00018E 4000                         299+         DC    BL2'0100000000000000'                                   *55800020
                                        +                                                 DSORG                  55890020
 000190 00000001                     300+         DC    A(1)                     IOBAD                           56340020

                                     302+*                       FOUNDATION EXTENSION                            56610020

 000194 00                           304+         DC    BL1'00000000'                  BFTEK,BFLN,HIARCHY        59850020
 000195 000001                       305+         DC    AL3(1)                  EODAD                            65970020
 000198 90                           306+         DC    BL1'10010000'                                           *66150020
                                        +                                       RECFM                            66240020
 000199 000000                       307+         DC    AL3(0)                  EXLST                            66330020

                                     309+*                       FOUNDATION BLOCK                                66690020

 00019C D3D6C74040404040             311+         DC    CL8'LOG'                DDNAME                           66870020
 0001A4 02                           312+         DC    BL1'00000010'           OFLGS                            68220020
 0001A5 00                           313+         DC    BL1'00000000'                          IFLG              68310020
 0001A6 0050                         314+         DC    BL2'0000000001010000'                                   *68400020
                                        +                                                                       *68490020
                                        +                                       MACR                             68580020

                                     316+*                       BSAM-BPAM-QSAM INTERFACE                        74430020

 0001A8 00                           318+         DC    BL1'00000000'                                           *74610020
                                        +                                                                   RER1 74700020
 0001A9 000001                       319+         DC    AL3(1)                  CHECK, GERR, PERR                74790020
 0001AC 00000244                     320+         DC    A(@FILWSYN)             SYNAD                            74880020
 0001B0 0000                         321+         DC    H'0'                    CIND1, CIND2                     74970020
 0001B2 0000                         322+         DC    AL2(0)                  BLKSIZE                          75240020
 0001B4 00000000                     323+         DC    F'0'                    WCPO, WCPL, OFFSR, OFFSW         75870020
 0001B8 00000001                     324+         DC    A(1)                    IOBA                             75960020
 0001BC 00                           325+         DC    AL1(0)                  NCP                              76050020
 0001BD 000001                       326+         DC    AL3(1)                  EOBR, EOBAD                      76140020

                                     328+*                            QSAM INTERFACE                             81450020

 0001C0 00000001                     330+         DC    A(1)                     RECAD                           81630020
 0001C4 0000                         331+         DC    H'0'                    QSWS                             81810020
 0001C6 0050                         332+         DC    AL2(80)        LRECL                                     80730020
 0001C8 00                           333+         DC    BL1'00000000'           EROPT                            82530020
 0001C9 000001                       334+         DC    AL3(1)                  CNTRL                            82620020
 0001CC 00000000                     335+         DC    F'0'                    PRECL                            82710020
 0001D0 00000001                     336+         DC    A(1)                    EOB                              82800020
1                                                                                                               PAGE   11
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
00001D8                              338          LTORG
1                                                                                                               PAGE   12
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                    340 ***********************************************************************
                                     341 *
                                     342 *        $PGM       PROGRAM DATA SECTION
                                     343 *
                                     344 ***********************************************************************

 000000                              346 $PGM     DSECT

 000000                              348          DS    18F                      REGISTER SAVE AREA

 000048                              350 PGM$I    DS    F                        @INIT RETURN ADDR
 00004C                              351 PGM$M    DS    F                        @MAIN RETURN ADDR
 000050                              352 PGM$F    DS    F                        @FINAL RETURN ADDR

 000054                              354 PGM$FIL  DS    F                        $FIL ADDR

 000058                              356 WCLOGREC DS    CL80                     OUTPUT RECORD

                             000A8   358 E$PGML   EQU   *-$PGM                   SIZE OF SAVE AREA

1                                                                                                               PAGE   13
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                    361 ***********************************************************************
                                     362 *
                                     363 *        INCLUDED OBJECTS
                                     364 *
                                     365 ***********************************************************************

                                     367          COPY  @FIL                     FILE CLASS
                                     368 ***********************************************************************
                                     369 *
                                     370 *        @FIL       FILE FACILITY
                                     371 *
                                     372 *        @FILA      ALLOCATE $FIL STORAGE
                                     373 *        @FILI      INITIALIZE $FIL
                                     374 *        @FILO      OPEN THE FILE
                                     375 *        @FILR      READ FROM THE FILE
                                     376 *        @FILW      WRITE TO THE FILE
                                     377 *        @FILC      CLOSE THE FILE
                                     378 *        @FILZ      RELEASE $FIL STORAGE
                                     379 *
                                     380 *        COPYRIGHT 2010-2021 DAVID J WALLING. MIT LICENSE.
                                     381 *
                                     382 ***********************************************************************
1                                                                                                               PAGE   14
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                    384 ***********************************************************************
                                     385 *
                                     386 *        @FIL       FILE CONTROL SECTION
                                     387 *
                                     388 ***********************************************************************

 0001D8                              390 @FIL     CSECT
1                                                                                                               PAGE   15
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                    392 ***********************************************************************
                                     393 *
                                     394 *        @FILA      ALLOCATE STORAGE FOR INSTANCE
                                     395 *
                                     396 *        IN         R10     @FIL CSECT ADDR
                                     397 *                   R12     CALLER CSECT ADDR
                                     398 *                   R13     CALLER DATA ADDR (SAVE AREA)
                                     399 *                   R14     RETURN ADDRESS
                                     400 *
                                     401 *        OUT        R1      $FIL ADDR (OR ABEND)
                                     402 *                   R15     GETMAIN RESULT (OR ABEND)
                                     403 *
                                     404 ***********************************************************************

 0001D8                              406 @FILA    DS    0H

                             001D8   408          USING @FIL,R10

 0001D8 182E                         410          LR    R2,R14                   HOLD RETURN ADDR
 0001DA 4100 0079      00079         411          LA    R0,E$FILL                $FIL SIZE

                                     413          GETMAIN R,LV=(0)               ALLOCATE STORAGE (,10)
 0001DE 4510 A00A      001E2         414+         BAL   1,*+4                             INDICATE GETMAIN
 0001E2 0A0A                         415+         SVC   10                                ISSUE GETMAIN SVC

 0001E4 18E2                         417          LR    R14,R2                   LOAD RETURN ADDR
 0001E6 07FE                         418          BR    R14                      RETURN TO CALLER

                                     420          DROP  R10
1                                                                                                               PAGE   16
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                    422 ***********************************************************************
                                     423 *
                                     424 *        @FILI      INITIALIZE $FIL CONTEXT
                                     425 *
                                     426 *        IN         R0      DCB OPTION BYTE (BITS 24-31)
                                     427 *                   R1      DCB TEMPLATE ADDR
                                     428 *                   R10     @FIL CSECT ADDR
                                     429 *                   R11     $FIL DSECT ADDR
                                     430 *                   R12     CALLER CSECT ADDR
                                     431 *                   R13     CALLER DATA ADDR (SAVE AREA)
                                     432 *                   R14     RETURN ADDRESS
                                     433 *
                                     434 ***********************************************************************

 0001E8                              436 @FILI    DS    0H

                             00000   438          USING $FIL,R11

 0001E8 50E0 B000      00000         440          ST    R14,FIL$I                SAVE RETURN ADDR (,11)

 0001EC 4200 B078      00078         442          STC   R0,FILXOPT               SAVE DCB OPTION BYTE (,11)
 0001F0 D25F B018 1000 00018 00000   443          MVC   FILXDCB,0(R1)            COPY DCB TEMPLATE (,11)

 0001F6 58E0 B000      00000         445          L     R14,FIL$I                LOAD RETURN ADDR (,11)
 0001FA 07FE                         446          BR    R14                      RETURN TO CALLER

                                     448          DROP  R11
1                                                                                                               PAGE   17
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                    450 ***********************************************************************
                                     451 *
                                     452 *        @FILO      OPEN THE FILE
                                     453 *
                                     454 *        IN         R10     @FIL CSECT ADDR
                                     455 *                   R11     $FIL DSECT ADDR
                                     456 *                   R12     CALLER CSECT ADDR
                                     457 *                   R13     CALLER DATA ADDR (SAVE AREA)
                                     458 *                   R14     RETURN ADDRESS
                                     459 *
                                     460 *        OUT        R15     OPEN RESULT CODE
                                     461 *
                                     462 ***********************************************************************

 0001FC                              464 @FILO    DS    0H

                             00000   466          USING $FIL,R11

 0001FC 50E0 B004      00004         468          ST    R14,FIL$O                SAVE RETURN ADDR (,11)

 000200 4110 B014      00014         470          LA    R1,FILFARG               PARMLIST ADDR (,11)
 000204 4100 B018      00018         471          LA    R0,FILXDCB               DCB ADDR (,11)
 000208 5000 1000      00000         472          ST    R0,0(,R1)                PUT DCB ADDR IN PARMLIST
 00020C D200 1000 B078 00000 00078   473          MVC   0(1,R1),FILXOPT          SET HI BYTE TO OPTION

 000212 0A13                         475          SVC   19                       ISSUE OPEN

 000214 58E0 B004      00004         477          L     R14,FIL$O                LOAD RETURN ADDR (,11)
 000218 07FE                         478          BR    R14                      RETURN TO CALLER

                                     480          DROP  R11
1                                                                                                               PAGE   18
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                    482 ***********************************************************************
                                     483 *
                                     484 *        @FILR      READ THE FILE
                                     485 *
                                     486 *        IN         R0      TARGET ADDR FOR READ
                                     487 *                   R10     @FIL CSECT ADDR
                                     488 *                   R11     $FIL DSECT ADDR
                                     489 *                   R12     CALLER CSECT ADDR
                                     490 *                   R13     CALLER DATA ADDR (SAVE AREA)
                                     491 *                   R14     RETURN ADDRESS
                                     492 *
                                     493 *        OUT        R15     GET RESULT CODE
                                     494 *
                                     495 *        GET ROUTINE MAY USE SAVE AREA AT R13
                                     496 *
                                     497 ***********************************************************************

 00021A                              499 @FILR    DS    0H

                             00000   501          USING $FIL,R11

 00021A 50E0 B008      00008         503          ST    R14,FIL$R                SAVE RETURN ADDR (,11)

 00021E 4110 B018      00018         505          LA    R1,FILXDCB               DCB ADDR (,11)
 000222 58F0 1030      00030         506          L     R15,48(,R1)              DCB GET ROUTINE ADDR

 000226 05EF                         508          BALR  R14,R15                  CALL GET ROUTINE

 000228 58E0 B008      00008         510          L     R14,FIL$R                LOAD RETURN ADDR (,11)
 00022C 07FE                         511          BR    R14                      RETURN TO CALLER

                                     513          DROP  R11
1                                                                                                               PAGE   19
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                    515 ***********************************************************************
                                     516 *
                                     517 *        @FILW      WRITE TO THE FILE
                                     518 *
                                     519 *        IN         R0      SOURCE ADDR FOR WRITE
                                     520 *                   R10     @FIL CSECT ADDR
                                     521 *                   R11     $FIL DSECT ADDR
                                     522 *                   R12     CALLER CSECT ADDR
                                     523 *                   R13     CALLER DATA ADDR (SAVE AREA)
                                     524 *                   R14     RETURN ADDRESS
                                     525 *
                                     526 *        OUT        R15     PUT RESULT CODE
                                     527 *
                                     528 *        PUT ROUTINE MAY USE SAVE AREA AT 13
                                     529 *
                                     530 ***********************************************************************

 00022E                              532 @FILW    DS    0H

                             001D8   534          USING @FIL,R10
                             00000   535          USING $FIL,R11

 00022E 50E0 B00C      0000C         537          ST    R14,FIL$W                SAVE RETURN ADDR (,11)

 000232 4110 B018      00018         539          LA    R1,FILXDCB               DCB ADDR (,11)
 000236 58F0 1030      00030         540          L     R15,48(,R1)              DCB PUT ROUTINE ADDR

 00023A 05EF                         542          BALR  R14,R15                  CALL PUT ROUTINE

 00023C 17FF                         544          XR    R15,R15                  ZERO RESULT IF NO SYNAD

 00023E                              546 @FILW80  DS    0H                       SYNAD RETURNS HERE

 00023E 58E0 B00C      0000C         548          L     R14,FIL$W                LOAD RETURN ADDR (,11)
 000242 07FE                         549          BR    R14                      RETURN TO CALLER

 000244                              551 @FILWSYN DS    0H                       SYNAD ENTRY

 000244 18F1                         553          LR    R15,R1                   ERROR BITS 0-7
 000246 88F0 0018      00018         554          SRL   R15,24                   ERROR BITS 24-31

 00024A 47F0 A066      0023E         556          B     @FILW80                  CONTINUE

                                     558          DROP  R10,R11
1                                                                                                               PAGE   20
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                    560 ***********************************************************************
                                     561 *
                                     562 *        @FILC      CLOSE THE FILE
                                     563 *
                                     564 *        IN         R10     @FIL CSECT ADDR
                                     565 *                   R11     $FIL DSECT ADDR
                                     566 *                   R12     CALLER CSECT ADDR
                                     567 *                   R13     CALLER DATA ADDR (SAVE AREA)
                                     568 *                   R14     RETURN ADDRESS
                                     569 *
                                     570 *        OUT        R15     SVC 20 RESULT CODE
                                     571 *
                                     572 ***********************************************************************

 00024E                              574 @FILC    DS    0H

                             00000   576          USING $FIL,R11

 00024E 50E0 B010      00010         578          ST    R14,FIL$C                SAVE RETURN ADDR (,11)

 000252 4110 B014      00014         580          LA    R1,FILFARG               PARMLIST ADDR (,11)
 000256 4100 B018      00018         581          LA    R0,FILXDCB               DCB ADDR (,11)
 00025A 5000 1000      00000         582          ST    R0,0(,R1)                PUT DCB ADDR IN PARMLIST
 00025E 9280 1000      00000         583          MVI   0(R1),X'80'              MARK AS LAST PARM

 000262 0A14                         585          SVC   20                       CLOSE FILE

 000264 58E0 B010      00010         587          L     R14,FIL$C                LOAD RETURN ADDR (,11)
 000268 07FE                         588          BR    R14                      RETURN TO CALLER

                                     590          DROP  R11
1                                                                                                               PAGE   21
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                    592 ***********************************************************************
                                     593 *
                                     594 *        @FILZ      FINALIZE $FIL OBJECT
                                     595 *
                                     596 *        IN         R10     @FIL CSECT ADDR
                                     597 *                   R11     $FIL DSECT ADDR
                                     598 *                   R12     CALLER CSECT ADDR
                                     599 *                   R13     CALLER DATA ADDR (SAVE AREA)
                                     600 *                   R14     RETURN ADDRESS
                                     601 *
                                     602 *        OUT        R15     FREEMAIN RESULT CODE (OR ABEND)
                                     603 *
                                     604 ***********************************************************************

 00026A                              606 @FILZ    DS    0H

                             001D8   608          USING @FIL,R10

 00026A 182E                         610          LR    R2,R14                   HOLD RETURN ADDR
 00026C 181B                         611          LR    R1,R11                   $FIL ADDR
 00026E 4100 0079      00079         612          LA    R0,E$FILL                $FIL LENGTH

                                     614          FREEMAIN R,LV=(0),A=(1)        RELEASE STORAGE (,10)
                                     615+*      OS/VS2 RELEASE 3 VERSION  -- 10/25/74                            00001603
 000272 4110 1000      00000         616+         LA    1,0(0,1)                          CLEAR HI ORDER BYTE    00150802
 000276 0A0A                         617+         SVC   10                                ISSUE FREEMAIN SVC     00311202

 000278 18E2                         619          LR    R14,R2                   LOAD RETURN ADDR
 00027A 07FE                         620          BR    R14                      RETURN TO CALLER

                                     622          DROP  R10
1                                                                                                               PAGE   22
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0                                    624 ***********************************************************************
                                     625 *
                                     626 *        W O R K I N G   S T O R A G E
                                     627 *
                                     628 ***********************************************************************

                             00080   630 FILETYPI EQU   X'80'                    INPUT FILE OPTION BYTE
                             0008F   631 FILETYPO EQU   X'8F'                    OUTPUT FILE OPTION BYTE

                                     633 ***********************************************************************
                                     634 *
                                     635 *        $FIL       FILE DATA SECTION
                                     636 *
                                     637 ***********************************************************************

 000000                              639 $FIL     DSECT

 000000                              641 FIL$I    DS    F                        @FILI RETURN ADDR
 000004                              642 FIL$O    DS    F                        @FILO RETURN ADDR
 000008                              643 FIL$R    DS    F                        @FILR RETURN ADDR
 00000C                              644 FIL$W    DS    F                        @FILW RETURN ADDR
 000010                              645 FIL$C    DS    F                        @FILC RETURN ADDR

 000014                              647 FILFARG  DS    F                        SVC 19,20 INPUT

 000018                              649          DS    0F                       FULLWORD ALIGN DCB
 000018                              650 FILXDCB  DS    XL96                     DCB COPY
 000078                              651 FILXOPT  DS    X                        OPTION BYTE

                             00079   653 E$FILL   EQU   *-$FIL                   DATA SECTION LENGTH
1                                                                                                               PAGE   23
-  LOC  OBJECT CODE    ADDR1 ADDR2  STMT   SOURCE STATEMENT                                       ASM 0201 20.15 12/17/20
0000000                              655          END   @PGM                     END OF PROGRAM
1                                                  RELOCATION DICTIONARY                                        PAGE   24
-POS.ID   REL.ID   FLAGS   ADDRESS                                                                ASM 0201 20.15 12/17/20
0 0001     0002      0C     000164
  0001     0002      0C     0001AC

1                                                  CROSS-REFERENCE                                              PAGE   25
-SYMBOL    LEN   VALUE   DEFN    REFERENCES                                                       ASM 0201 20.15 12/17/20
0$FIL     00001 00000000 00639  00438 00466 00501 00535 00576 00653
 $PGM     00001 00000000 00346  00031 00092 00160 00202 00358
 @FIL     00001 000001D8 00390  00093 00161 00203 00279 00408 00534 00608
 @FILA    00002 000001D8 00406  00098
 @FILC    00002 0000024E 00574  00209
 @FILI    00002 000001E8 00436  00115
 @FILO    00002 000001FC 00464  00128
 @FILR    00002 0000021A 00499
 @FILW    00002 0000022E 00532  00172
 @FILWSYN 00002 00000244 00551  00320
 @FILW80  00002 0000023E 00546  00556
 @FILZ    00002 0000026A 00606  00227
 @FINAL   00002 00000110 00199  00061
 @FINL10  00002 0000013A 00223  00212
 @INIT    00002 00000058 00089  00052
 @INIT10  00002 000000C8 00144  00131
 @MAIN    00002 000000CE 00157  00057
 @MAIN10  00002 0000010A 00186  00175
 @PGM     00001 00000000 00028  00030 00091 00159 00201 00655
 @PGM10   00002 00000034 00059  00055
 CCLOGMSG 00011 00000168 00281  00167 00167
 DCBLOG   00004 00000174 00287  00112
 E$FILL   00001 00000079 00653  00411 00612
 E$PGML   00001 000000A8 00358  00036 00043 00065
 FIL$C    00004 00000010 00645  00578 00587
 FIL$I    00004 00000000 00641  00440 00445
 FIL$O    00004 00000004 00642  00468 00477
 FIL$R    00004 00000008 00643  00503 00510
 FIL$W    00004 0000000C 00644  00537 00548
 FILETYPI 00001 00000080 00630
 FILETYPO 00001 0000008F 00631  00111
 FILFARG  00004 00000014 00647  00470 00580
 FILXDCB  00096 00000018 00650  00443 00471 00505 00539 00581
 FILXOPT  00001 00000078 00651  00442 00473
 IHB0003A 00002 00000078 00108  00104
 IHB0004A 00002 000000A0 00123  00119
 IHB0005A 00002 000000C4 00139  00135
 IHB0006A 00002 00000108 00183  00179
 IHB0007A 00002 00000138 00220  00216
 IHB0008A 00002 00000158 00235  00231
 PGM$F    00004 00000050 00352  00205 00240
 PGM$FIL  00004 00000054 00354  00100 00114 00127 00171 00208 00226
 PGM$I    00004 00000048 00350  00095 00146
 PGM$M    00004 0000004C 00351  00163 00188
 PGM@FIL  00004 00000164 00279  00097 00113 00126 00170 00207 00225
 R0       00001 00000000 00262  00036 00065 00071 00077 00111 00169 00411 00442 00471 00472 00581 00582 00612
 R1       00001 00000001 00263  00042 00048 00049 00050 00066 00067 00072 00100 00112 00443 00470 00472 00473 00505 00506
                                00539 00540 00553 00580 00582 00583 00611
 R10      00001 0000000A 00272  00093 00097 00113 00126 00149 00161 00170 00191 00203 00207 00225 00243 00408 00420 00534
                                00558 00608 00622
 R11      00001 0000000B 00273  00114 00127 00171 00208 00226 00438 00448 00466 00480 00501 00513 00535 00558 00576 00590
                                00611
 R12      00001 0000000C 00274  00030 00033 00034 00077 00081 00091 00149 00159 00191 00201 00243
 R13      00001 0000000D 00275  00031 00033 00048 00049 00050 00066 00067 00077 00078 00081 00092 00149 00160 00191 00202
                                00243
1                                                  CROSS-REFERENCE                                              PAGE   26
-SYMBOL    LEN   VALUE   DEFN    REFERENCES                                                       ASM 0201 20.15 12/17/20
0R14      00001 0000000E 00276  00033 00042 00046 00052 00057 00061 00078 00079 00095 00098 00115 00128 00146 00147 00163
                                00172 00188 00189 00205 00209 00227 00240 00241 00410 00417 00418 00440 00445 00446 00468
                                00477 00478 00503 00508 00510 00511 00537 00542 00548 00549 00578 00587 00588 00610 00619
                                00620
 R15      00001 0000000F 00277  00034 00043 00054 00054 00063 00075 00130 00130 00142 00142 00174 00174 00211 00211 00238
                                00238 00506 00508 00540 00542 00544 00544 00553 00554
 R2       00001 00000002 00264  00044 00044 00045 00046 00063 00075 00410 00417 00610 00619
 R3       00001 00000003 00265  00045
 R4       00001 00000004 00266
 R5       00001 00000005 00267
 R6       00001 00000006 00268
 R7       00001 00000007 00269
 R8       00001 00000008 00270
 R9       00001 00000009 00271
 WCLOGREC 00080 00000058 00356  00165 00166 00166 00166 00167 00169
1                                                  ASSEMBLER DIAGNOSTICS AND STATISTICS                         PAGE   27
-                                                                                                 ASM 0201 20.15 12/17/20
0NO STATEMENTS FLAGGED IN THIS ASSEMBLY
 HIGHEST SEVERITY WAS    0
 OPTIONS FOR THIS ASSEMBLY
   ALIGN, ALOGIC, BUFSIZE(STD), DECK, ESD, FLAG(0), LINECOUNT(55), LIST, NOMCALL, YFLAG, WORKSIZE(2097152)
   NOMLOGIC, NONUMBER, OBJECT, RENT, RLD, NOSTMT, NOLIBMAC, NOTERMINAL, NOTEST, XREF
   SYSPARM()
 WORK FILE BUFFER SIZE/NUMBER =19066/ 1
 TOTAL RECORDS READ FROM SYSTEM INPUT        242
 TOTAL RECORDS READ FROM SYSTEM LIBRARY     4240
 TOTAL RECORDS PUNCHED                        15
 TOTAL RECORDS PRINTED                       785
```

### Linker Messages

The linker finds two CSECTs, @PGM and @FIL. The program entry is offset 00 into the @PGM CSECT. The module is reentrant and reusable.
```
 F64-LEVEL LINKAGE EDITOR OPTIONS SPECIFIED XREF,LET,LIST,NCAL,RENT                                                  
          DEFAULT OPTION(S) USED -  SIZE=(229376,55296)                                                              
                                                                                                                     
                                                CROSS REFERENCE TABLE                                                
                                                                                                                     
  CONTROL SECTION                       ENTRY                                                                        
    NAME    ORIGIN  LENGTH                NAME   LOCATION     NAME   LOCATION     NAME   LOCATION     NAME   LOCATION
  @PGM          00     1D8                                                                                           
  @FIL         1D8      A4                                                                                           
                                                                                                                     
                                                                                                                     
  LOCATION  REFERS TO SYMBOL  IN CONTROL SECTION             LOCATION  REFERS TO SYMBOL  IN CONTROL SECTION          
      164            @FIL            @FIL                         1AC            @FIL            @FIL                
                                                                                                                     
 ENTRY ADDRESS       00                                                                                              
 TOTAL LENGTH       280                                                                                              
****ASM005    NOW REPLACED IN DATA SET                                                                               
AUTHORIZATION CODE IS         0.                                                                                     
**MODULE HAS BEEN MARKED REENTERABLE, AND REUSABLE.                                                                  
```     

### Operator Console Output

Program progress notifcations are written to the operator console using the WTO macro.
```
20.15.33 JOB  155  $HASP100 ASM005   ON INTRDR      DWALL01
20.15.33 JOB  155  $HASP373 ASM005   STARTED - INIT  1 - CLASS A - SYS HMVS
20.15.33 JOB  155  IEF403I ASM005 - STARTED - TIME=20.15.33
20.15.36 JOB  155  IEFACTRT ASM     /IFOX00  /00:00:02.38/00:00:03.00/00000/ASM005
20.15.36 JOB  155  IEFACTRT LINK    /IEWL    /00:00:00.20/00:00:00.37/00000/ASM005
20.15.36 JOB  155  +@FILA OK
20.15.37 JOB  155  +@FILI OK
20.15.37 JOB  155  +@FILO OK
20.15.37 JOB  155  +@FILW OK
20.15.37 JOB  155  +@FILC OK
20.15.37 JOB  155  +@FILZ OK
20.15.37 JOB  155  IEFACTRT RUN     /ASM005  /00:00:00.12/00:00:00.40/00000/ASM005
20.15.37 JOB  155  IEF404I ASM005 - ENDED - TIME=20.15.37
20.15.37 JOB  155  $HASP395 ASM005   ENDED
20.15.37           $HASP309    INIT  1 INACTIVE ******** C=A
20.15.37 JOB  155  $HASP150 ASM005   ON PUNCH1          15 CARDS
20.15.37 JOB  155  $HASP150 ASM005   ON PRINTER1       184 LINES
20.15.37           $HASP160 PUNCH1   INACTIVE - CLASS=B
20.15.37           $HASP160 PRINTER1 INACTIVE - CLASS=A
20.15.37 JOB  155  $HASP250 ASM005   IS PURGED
```

### Output Log File (GDG Generation)

```
 View DWALL01.GDG.LOG.G0022V00--------------------------------------------------
 Cmd =>                                                                         
----+----1----+----2----+----3----+----4----+----5----+----6----+----7----+----8
LOG MESSAGE                                                                     
```

<hr>

Return to [Sample Programs](Samples.md)  
Return to [README](../README.md)
